#!/usr/bin/env gurax
import(util.tester) {*}

TestCase('Basic test') {
	test(str:String) = {
		Printf('\'%s\' -> \'%s\'\n', str, str.Embed())
		Println()
	}
	test('')
	test('a')
	test(R'''${repeat (10){|i|}}item#${i}${end}''')
	test(R'''
	${repeat (10){|i|}}
	item#${i}
	${end}
	''')
	test(R'''
	${repeat (10){|i|}}item#${i}
	${end}
	''')
	test(R'''
	${repeat (10){|i|}}
	  item#${i}
	${end}
	''')
	test(R'''${for (i in 0..9)}item${i}${end}''')
	test(R'''
	${for (i in 0..9)}
	item${i}
	${end}''')
	test(R'''
	${for (i in 0..9)}
	  item${i}
	${end}
	''')
	test(R'''${for (i in 0..9)}${if (i % 2 == 0)}item${i}${end}${end}''')
	test(R'''
	${for (i in 0..9)}
	  ${if (i % 2 == 0)}
	    item${i}
	  ${end}
	${end}''')
	test(R'''
	${for (i in 0..9)}
	  ${if (i % 2 == 0)}
	    ${i} is even
	  ${else}
	    ${i} is odd
	  ${end}
	${end}''')
	test(R'''
	${for (i in 0..9)}
	  ${if (i % 2 == 0)}
	    ${i} is a multiple of two
	  ${elsif (i % 3 == 0)}
	    ${i} is a multiple of three
	  ${else}
	    ${i} is another
	  ${end}
	${end}''')
}
//-----------------------------------------------------------------------------
TestCase('marker handling') {
	x = 'hoge'
	R'''
	$
	$$
	$$$
	$$$$
	$$$$$
	$$$$$$
	$abc
	$0
	${x}
	$${x}
	$$${x}
	$$$${x}
	${x}$
	${x}$$
	'''.Embed(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('Reference to external variables') {
	v1 = 'abcde'
	v2 = 'fghij'
	v3 = 'klmno'
	R'''
	${v1} ${v2} ${v3}
	${v1 += ' AA'}
	'''.Embed(sys.cout)
	Tester(`v1)
}
//-----------------------------------------------------------------------------
TestCase('Comment') {
	R'''
	  ${// comment}
	  line.1
	    ${# comment}
	  line.2
	      ${/* comment */}
	  line.3
	  line.4${//comment}
	
	'''.Embed(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('Block paramter') {
	R'''
	${(1..10).Each {|i|}}
	  ${i}
	${end}
	'''.Embed(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('Template script that returns nil value') {
	R'''
	hello
	${
	nil
	}
	${
	-
	}
	${nil}
	${-}
	gura
	${''}
	world
	
	hello ${nil}world
	hello ${-}world
	hello ${(1..10) + ','} world
	hello ${(1..10) + '\n'} world
	foo${nil}bar
	foo${-}bar
	foo${''}bar
	'''.Embed(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('Repeat sequence') {
	companies = ['Gura company', 'Guri corporation', nil]
	products = ['apple', 'grape', 'orange']
	// iteration with for statement
	R'''
	[Companies]
	${for (company in companies)}
	  ${if (company)}
	    Company: ${company}
	  ${else}
	    Company: (not defined)
	  ${end}
	${end}
	[Products]
	${for (product in products)}
	- ${product}
	${end}
	'''.Embed(sys.cout)
	// iteration with Each() method
	R'''
	[Companies]
	${companies.Each {|company|}}
	  ${if (company)}
	    Company: ${company}
	  ${else}
	    Company: (not defined)
	  ${end}
	${end}
	[Products]
	${products.Each {|product|}}
	- ${product}
	${end}
	'''.Embed(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('Repeat and if-else sequence') {
	R'''
	${for (i in 1..10)}
	  ${if (i < 5)}
	    ${i} is less than five
	  ${elsif (i < 8)}
	    ${i} is less than eight
	  ${else}
	    ${i} is greater than or equal to eight
	  ${end}
	${end}
	'''.Embed(sys.cout)
}
/*
//-----------------------------------------------------------------------------
TestCase() {
	println('漢字のテスト ${"スクリプト文字列"}'.Embed())
}
*/
//-----------------------------------------------------------------------------
TestCase('Template script that handles template internally') {
tmpl = R'''
${if (company) {R"""
Company: ${company}
""".Embed()} else {R"""
(not defined)
"""}}
[Products]
${R"""
- %s
""".Format(products)}
'''
	scope {
		company = 'hoge'
		products = ['apple', 'grape', 'orange']
		tmpl.Embed(sys.cout)
		company = nil
		products = ['car', 'bicycle', 'boat']
		tmpl.Embed(sys.cout)
	}
}
//-----------------------------------------------------------------------------
TestCase('Empty list') {
	R'''
	hello
	${
	[]
	}
	${[]}
	gura
	${''}
	world
	${[]}
	!!!
	ABCD${[]}EFG
	'''.Embed(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('Empty iterator') {
	R'''
	hello
	${
	()
	}
	${()}
	gura
	${''}
	world
	${()}
	!!!
	ABCD${()}EFG
	'''.Embed(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('Auto indentation') {
	x = Format('embedded #%d line\n', 1..4):list
	tmpl = R'''
	space 1
	 ${x}
	space 2
	  ${x}
	space 3
	   ${x}
	space 4
	    ${x}
	tab 1
		${x}
	tab 2
			${x}
	not line top:  ${x}
	'''
	Println('** auto-indent **')
	tmpl.Template().Render(sys.cout)
	Println('** append last-eol **')
	(tmpl.Template():lasteol).Render(sys.cout)
	Println('** no-indent **')
	(tmpl.Template():noindent).Render(sys.cout)
	Println('** no-indent, append last-eol **')
	(tmpl.Template():noindent:lasteol).Render(sys.cout)
	x = 'single string'
	tmpl.Template().Render(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('Error in template') {
	try {
		'abcdefg${not_defined}hijklmn'.Embed()
	} catch {|e|
		Println('expected error occurs: ', e.text)
	}
}
//-----------------------------------------------------------------------------
TestCase('Format() in tempate script') {
	Tester(`'hello ${Format("%d", 0..10).Join(",")}'.Embed())
}
//-----------------------------------------------------------------------------
TestCase('Variable reference') {
	var = 'HOGE'
	Tester(`'hello ${var} world'.Embed())
	Tester(`'hello ${var * 2} world'.Embed())
	Tester(`'''hello '${Format('%8s', var)}' world'''.Embed())
	var = 3.14
	Tester(`'hello ${var} world'.Embed())
}
//-----------------------------------------------------------------------------
TestCase('nil, number, string, and list') {
	Tester(`'${nil}'.Embed())
	Tester(`'${-}'.Embed())
	Tester(`'${3}'.Embed())
	Tester(`'${"hello"}'.Embed())
	Tester(`'${["first", "second", "third"]}'.Embed())
	try {
		'${Color.red}'.Embed()
	} catch {|e|
		Println('expected error occurs: ', e.text)
	}
}
//-----------------------------------------------------------------------------
TestCase('branch sequence') {
	R'''
	${x = 10}
	${x}
	hello world
	  ${x = 100, -}
	  ${if (x < 10)}
	    A
	  ${else}
	    B
	  ${end}
	  ${for (x in 1..8)}
	    ${for (y in 0..9)}${y}${end}
	  ${end}

	----
	  ${for (x in 1..8)}
	    ${for (y in 0..9)}${y}${end, ''}
	  ${end}
	hoge
	
	${for (x in 1..10)}
	    hoge ${x}
	${end}
	
	'''.Embed(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('test of scope') {
	x = '${value}\n'.Template()
	try {
		x.Render(sys.cout)
	} catch {|e|
		Println('expected error occurs: ', e.text)
	}
	value = 'hoge'
	x.Render(sys.cout)
	scope {
		value = 'hoge-inside'
		x.Render(sys.cout)
		scope {
			value = 'hoge-inside-inside'
			x.Render(sys.cout)
		}
	}
}
//-----------------------------------------------------------------------------
TestCase('Rendering HTML') {
	tmpl = R'''
	<h2>${title}</h2>
	<p>number of people: ${people.len}</p>
	
	<table>
	  <tr><th>No</th><th>Name</th><th>Age</th></tr>
	${for (person in people) {|i|}}
	  <tr><td>${i + 1}</td><td>${person.name}</td><td>${person.age}</td></tr>
	${end}
	</table>
	
	<table>
	  <tr><th>No</th><th>Name</th><th>Age</th></tr>
	${people.Each {|person, i|}}
	  <tr><td>${i + 1}</td><td>${person.name}</td><td>${person.age}</td></tr>
	${end}
	</table>
	
	<table>
	  <tr><th>No</th><th>Name</th><th>Age</th></tr>
	  ${Format('<tr><td>%d</td><td>%s</td><td>%d</td></tr>\n',
	  										1.., people:*name, people:*age)}
	</table>
	'''.Template()
	scope {
		Person = struct { name:string, age:number }
		people = [
			Person('Yamada', 23), Person('Honda', 21), Person('Tanaka', 24)
		]
		title = 'Member List'
		tmpl.Render(sys.cout)
	}
}
//-----------------------------------------------------------------------------
TestCase('test of comment') {
	tmpl = R'''
	1st line
	2nd line
	3rd line
	${==
	*comment-out*
	*comment-out*
	*comment-out*
	==}$
	4th line
	${==
	*comment-out*
	${=*comment-out*
	*comment-out*==
	*comment-out*=}
	*comment-out*
	==}$
	5th line
	${==
	${==*comment-out*
	==}$
	6th line
	${==*comment-out*==}$
	7th line
	8th line
	9th ${==*comment-out*==}$line
	10th line${==*comment-out*==}$
	11th line
	'''.Embed(sys.cout)
}
/*
//-----------------------------------------------------------------------------
TestCase('test of inheritance') {
	// base1 template
	base1 = R'''
	=== ${=block(`head)}base1-head${end} ===
	<start-of-body>
	${=block(`body)}
	base1-body
	${end}
	<end-of-body>
	'''.Template()
	// base2 template
	base2 = R'''
	${=extends(base1)}
	
	${=block(`head)}base2-head${end}
	
	${=block(`body)}
	${end}
	'''.Template()
	// base3 template
	base3 = R'''
	${=extends(base2)}
	
	${=block(`head)}base3-head${end}
	
	${=block(`body)}
	base3-body
	${end}
	'''.Template()
	Println('inherit from base1')
	R'''
	${=extends(base1)}
	'''.Embed(sys.cout)
	Println('inherit from base2')
	R'''
	${=extends(base2)}
	'''.Embed(sys.cout)
	Println('inherit from base3')
	R'''
	${=extends(base3)}
	'''.Embed(sys.cout)
}
*/
/*
//-----------------------------------------------------------------------------
TestCase('test of embed') {
	//--------
	tmpl1 = R'''
	This is a rendering result of tmpl1
	'''.Template()
	//--------
	tmpl2 = R'''
	This is a rendering result of tmpl2
	'''.Template()
	//--------
	tmpl3 = R'''
	This is a rendering result of tmpl3
	'''.Template()
	//--------
	R'''
	embed tmpl1
	${=embed(tmpl1)}
	embed tmpl2
	${=embed(tmpl2)}
	embed tmpl3
	${=embed(tmpl3)}
	'''.Embed(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('test of local variable in embed') {
	//--------
	tmpl = R'''
	${x = 'embed', -}
	x = ${x}
	'''.Template()
	//--------
	R'''
	${x = 'body', -}
	x = ${x}
	embed tmpl
	${=embed(tmpl)}
	x = ${x}
	'''.Embed(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('test of detecting duplicated symbol') {
	tmpl = R'''
	${=block(`hoge1)}${end}
	${=block(`hoge2)}${end}
	${=block(`hoge3)}${end}
	${=block(`hoge4)}${end}
	${=block(`hoge1)}${end}
	'''.Template()
	try {
		tmpl.render(sys.cout)
	} catch {|e|
		Println('expected error occurs: ', e.text)
	}
}
//-----------------------------------------------------------------------------
TestCase('test of inheritance') {
	// this sample originated from an example in http://jinja.pocoo.org/.
	base = R'''
	<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01//EN">
	<html lang="en">
	<html xmlns="http://www.w3.org/1999/xhtml">
	<head>
	    ${=block(`head)}
	    <link rel="stylesheet" href="style.css" />
	    <title>${=block(`title)}${end} - My Webpage</title>
	    ${end}
	</head>
	<body>
	    <div id="content">
	    ${=block(`content)}${end}
	    </div>
	    <div id="footer">
	        ${=block(`footer)}
	        &copy; Copyright 2008 by <a href="http://domain.invalid/">you</a>.
	        ${end}
	    </div>
	</body>
	'''.Template()
	R'''
	${=extends(base)}
	${=block(`title)}Index${end}
	${=block(`head)}
	    ${=super(`head)}
	    <style type="text/css">
	        .important { color: #336699; }
	    </style>
	${end}
	${=block(`content)}
	    <h1>Index</h1>
	    <p class="important">
	      Welcome on my awesome homepage.
	    </p>
	${end}
	'''.Embed(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('test of inheritance and loop') {
	// this sample originated from an example in http://jinja.pocoo.org/.
	Users = struct(username:string, url:string)
	users = @(Users) {
		{ 'Iwai Michiko',		'iwai_michiko@example.com'		},
		{ 'Kasai Satoshi',		'kasai_satoshi@example.com'		},
		{ 'Shima Masashi',		'shima_masashi@example.com'		},
		{ 'Suenaga Rena',		'suenaga_rena@example.com'		},
		{ 'Yokokawa Fumiyo',	'yokokawa_fumiyo@example.com'	},
		{ 'Yamanaka Mayuko',	'yamanaka_mayuko@example.com'	},
	}
	layout = R'''
	<html>
	<body>
	${=block(`body)}${end}
	</body>
	</html>
	'''.Template()
	R'''
	${=extends(layout)}
	${=block(`body)}
	  <ul>
	  ${for (user in users)}
	    <li><a href="${user.url}">${user.username}</a></li>
	  ${end}
	  </ul>
	${end}
	'''.Embed(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('define and call') {
	R'''
	${=define(`calc_mean, num1:number, num2:number)}${(num1 + num2) / 2}${end}
	${=define(`show_person, name:string, age:number)}
	${name} is ${age} years old.
	${end}
	${=call(`show_person, 'Harry', 24)}
	
	[${=call(`calc_mean, 12, 23)}]
	'''.Embed(sys.cout)
}

//-----------------------------------------------------------------------------
TestCase('macro and line-break') {
	R'''
	${=define(`macro1)}${end}
	${=define(`macro2)}Gura${end}
	${=define(`macro3)}
	Gura${end}
	${=define(`macro4)}Gura
	${end}
	${=define(`macro5)}
	Gura
	${end}
	Empty${=call(`macro1)}Here
	NoLineBreak${=call(`macro2)}Here
	NoLineBreak${=call(`macro3)}Here
	LineBreak${=call(`macro4)}Here
	LineBreak${=call(`macro5)}Here
	Macro:${=call(`macro2)}
	Next Line
	'''.Embed(sys.cout)
}


//-----------------------------------------------------------------------------
TestCase('embedded string') {
	var = 'hoge'
	Tester(`e'')
	Tester(`e'simple string')
	Tester(`e'variable ${var} reference')
	Tester(`eR'''
	variable ${var} reference
	''')
	// inheritance
	base = '[${=block(`head)}original${end}] ${=block(`body)}original${end}'.Template()
	Tester(`e'${=extends(base)}')
	Tester(`e'${=extends(base)}${=block(`head)}replaced${end}')
	Tester(`e'${=extends(base)}${=block(`body)}replaced${end}')
	Tester(`e'${=extends(base)}${=block(`head)}replaced${end}${=block(`body)}replaced${end}')
	f(a, b) = e'${a} + ${b} = ${a + b}'
	Tester(`f(3, 4))
	Tester(`f(10, 2))
}

//-----------------------------------------------------------------------------
TestCase('suffix T') {
	Tester(`''T)
	Tester(`'${hoge}'T)
}
*/
