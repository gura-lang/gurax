#!/usr/bin/env gurax
import(util.tester) {*}

TestCase('Basic test') {
	test(str:String) = {
		Printf('\'%s\' -> \'%s\'\n', str, str.Template().Render())
		Println()
	}
	test('')
	test('a')
	test(R'''${repeat (10){|i|}}item#${i}${end}''')
	test(R'''
	${repeat (10){|i|}}
	item#${i}
	${end}
	''')
	test(R'''
	${repeat (10){|i|}}item#${i}
	${end}
	''')
	test(R'''
	${repeat (10){|i|}}
	  item#${i}
	${end}
	''')
	test(R'''${for (i in 0..9)}item${i}${end}''')
	test(R'''
	${for (i in 0..9)}
	item${i}
	${end}''')
	test(R'''
	${for (i in 0..9)}
	  item${i}
	${end}
	''')
	test(R'''${for (i in 0..9)}${if (i % 2 == 0)}item${i}${end}${end}''')
	test(R'''
	${for (i in 0..9)}
	  ${if (i % 2 == 0)}
	    item${i}
	  ${end}
	${end}''')
	test(R'''
	${for (i in 0..9)}
	  ${if (i % 2 == 0)}
	    ${i} is even
	  ${else}
	    ${i} is odd
	  ${end}
	${end}''')
	test(R'''
	${for (i in 0..9)}
	  ${if (i % 2 == 0)}
	    ${i} is a multiple of two
	  ${elsif (i % 3 == 0)}
	    ${i} is a multiple of three
	  ${else}
	    ${i} is another
	  ${end}
	${end}''')
}
//-----------------------------------------------------------------------------
TestCase('suffix T') {
	Tester(`T'')
	Tester(`T'${hoge}')
}
//-----------------------------------------------------------------------------
TestCase('marker handling') {
	x = 'hoge'
	tR'''
	$
	$$
	$$$
	$$$$
	$$$$$
	$$$$$$
	$abc
	$0
	${x}
	$${x}
	$$${x}
	$$$${x}
	${x}$
	${x}$$
	'''.Render(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('Reference to external variables') {
	v1 = 'abcde'
	v2 = 'fghij'
	v3 = 'klmno'
	tR'''
	${v1} ${v2} ${v3}
	${v1 += ' AA'}
	'''.Render(sys.cout)
	Tester(`v1)
}
//-----------------------------------------------------------------------------
TestCase('Comment') {
	tR'''
	  ${// comment}
	  line.1
	    ${# comment}
	  line.2
	      ${/* comment */}
	  line.3
	  line.4${//comment}
	
	'''.Render(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('Block paramter') {
	tR'''
	${(1..10).Each {|i|}}
	  ${i}
	${end}
	'''.Render(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('Template script that returns nil value') {
	tR'''
	hello
	${
	nil
	}
	${
	-
	}
	${nil}
	${-}
	gura
	${''}
	world
	
	hello ${nil}world
	hello ${-}world
	hello ${(1..10) + ','} world
	hello ${(1..10) + '\n'} world
	foo${nil}bar
	foo${-}bar
	foo${''}bar
	'''.Render(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('Repeat sequence') {
	companies = ['Gura company', 'Guri corporation', nil]
	products = ['apple', 'grape', 'orange']
	// iteration with for statement
	tR'''
	[Companies]
	${for (company in companies)}
	  ${if (company)}
	    Company: ${company}
	  ${else}
	    Company: (not defined)
	  ${end}
	${end}
	[Products]
	${for (product in products)}
	- ${product}
	${end}
	'''.Render(sys.cout)
	// iteration with Each() method
	tR'''
	[Companies]
	${companies.Each {|company|}}
	  ${if (company)}
	    Company: ${company}
	  ${else}
	    Company: (not defined)
	  ${end}
	${end}
	[Products]
	${products.Each {|product|}}
	- ${product}
	${end}
	'''.Render(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('Repeat and if-else sequence') {
	tR'''
	${for (i in 1..10)}
	  ${if (i < 5)}
	    ${i} is less than five
	  ${elsif (i < 8)}
	    ${i} is less than eight
	  ${else}
	    ${i} is greater than or equal to eight
	  ${end}
	${end}
	'''.Render(sys.cout)
}
/*
//-----------------------------------------------------------------------------
TestCase() {
	Println(t'漢字のテスト ${"スクリプト文字列"}'.Render())
}
*/
//-----------------------------------------------------------------------------
TestCase('Template script that handles template internally') {
	tmpl = tR'''
	${if (company) {tR"""
	Company: ${company}
	""".Render()} else {R"""
	(not defined)
	"""}}
	[Products]
	${R"""
	- %s
	""".Format(products)}
	'''
	scope {
		company = 'hoge'
		products = ['apple', 'grape', 'orange']
		tmpl.Render(sys.cout)
		company = nil
		products = ['car', 'bicycle', 'boat']
		tmpl.Render(sys.cout)
	}
}
//-----------------------------------------------------------------------------
TestCase('Empty list') {
	tR'''
	hello
	${
	[]
	}
	${[]}
	gura
	${''}
	world
	${[]}
	!!!
	ABCD${[]}EFG
	'''.Render(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('Empty iterator') {
	tR'''
	hello
	${
	()
	}
	${()}
	gura
	${''}
	world
	${()}
	!!!
	ABCD${()}EFG
	'''.Render(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('Auto indentation') {
	x = Format('embedded #%d line\n', 1..4):list
	tmpl = R'''
	space 1
	 ${x}
	space 2
	  ${x}
	space 3
	   ${x}
	space 4
	    ${x}
	tab 1
		${x}
	tab 2
			${x}
	not line top:  ${x}
	'''
	Println('** auto-indent **')
	tmpl.Template().Render(sys.cout)
	Println('** append last-eol **')
	(tmpl.Template():lasteol).Render(sys.cout)
	Println('** no-indent **')
	(tmpl.Template():noindent).Render(sys.cout)
	Println('** no-indent, append last-eol **')
	(tmpl.Template():noindent:lasteol).Render(sys.cout)
	x = 'single string'
	tmpl.Template().Render(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('Error in template') {
	try {
		t'abcdefg${not_defined}hijklmn'.Render()
	} catch {|e|
		Println('expected error occurs: ', e.text)
	}
}
//-----------------------------------------------------------------------------
TestCase('Format() in tempate script') {
	Tester(`t'hello ${Format("%d", 0..10).Join(",")}'.Render())
}
//-----------------------------------------------------------------------------
TestCase('Variable reference') {
	var = 'HOGE'
	Tester(`t'hello ${var} world'.Render())
	Tester(`t'hello ${var * 2} world'.Render())
	Tester(`t'''hello '${Format('%8s', var)}' world'''.Render())
	var = 3.14
	Tester(`t'hello ${var} world'.Render())
}
//-----------------------------------------------------------------------------
TestCase('nil, number, string, and list') {
	Tester(`t'${nil}'.Render())
	Tester(`t'${-}'.Render())
	Tester(`t'${3}'.Render())
	Tester(`t'${"hello"}'.Render())
	Tester(`t'${["first", "second", "third"]}'.Render())
	try {
		t'${Color.red}'.Render()
	} catch {|e|
		Println('expected error occurs: ', e.text)
	}
}
//-----------------------------------------------------------------------------
TestCase('Loop and branch sequence') {
	tR'''
	${x = 10}
	${x}
	hello world
	  ${x = 100, -}
	  ${if (x < 10)}
	    A
	  ${else}
	    B
	  ${end}
	  ${for (x in 1..8)}
	    ${for (y in 0..9)}${y}${end}
	  ${end}

	----
	  ${for (x in 1..8)}
	    ${for (y in 0..9)}${y}${end, ''}
	  ${end}
	hoge
	
	${for (x in 1..10)}
	    hoge ${x}
	${end}
	
	'''.Render(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('Scope') {
	x = T'${value}\n'
	try {
		x.Render(sys.cout)
	} catch {|e|
		Println('expected error occurs: ', e.text)
	}
	value = 'hoge'
	x.Render(sys.cout)
	scope {
		value = 'hoge-inside'
		x.Render(sys.cout)
		scope {
			value = 'hoge-inside-inside'
			x.Render(sys.cout)
		}
	}
}
//-----------------------------------------------------------------------------
TestCase('Rendering HTML') {
	tmpl = tR'''
	<h2>${title}</h2>
	<p>number of people: ${people.len}</p>
	
	<table>
	  <tr><th>No</th><th>Name</th><th>Age</th></tr>
	${for (person in people) {|i|}}
	  <tr><td>${i + 1}</td><td>${person.name}</td><td>${person.age}</td></tr>
	${end}
	</table>
	
	<table>
	  <tr><th>No</th><th>Name</th><th>Age</th></tr>
	${people.Each {|person, i|}}
	  <tr><td>${i + 1}</td><td>${person.name}</td><td>${person.age}</td></tr>
	${end}
	</table>
	
	<table>
	  <tr><th>No</th><th>Name</th><th>Age</th></tr>
	  ${Format('<tr><td>%d</td><td>%s</td><td>%d</td></tr>\n',
	  										1.., people:*name, people:*age)}
	</table>
	'''
	scope {
		Person = struct { name:string, age:number }
		people = @(Person) {{'Yamada', 23}, {'Honda', 21}, {'Tanaka', 24}}
		title = 'Member List'
		tmpl.Render(sys.cout)
	}
}
//-----------------------------------------------------------------------------
TestCase('Comment') {
	tmpl = tR'''
	1st line
	2nd line
	3rd line
	${==
	*comment-out*
	*comment-out*
	*comment-out*
	==}$
	4th line
	${==
	*comment-out*
	${=*comment-out*
	*comment-out*==
	*comment-out*=}
	*comment-out*
	==}$
	5th line
	${==
	${==*comment-out*
	==}$
	6th line
	${==*comment-out*==}$
	7th line
	8th line
	9th ${==*comment-out*==}$line
	10th line${==*comment-out*==}$
	11th line
	'''.Render(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('Macro') {
	tR'''
	${=define(`calc_mean, num1:Number, num2:Number)}${(num1 + num2) / 2}${end}
	${=define(`show_person, name:String, age:Number)}
	${name} is ${age} years old.
	${end}

	${=call(`show_person, 'Harry', 24)}
	[${=call(`calc_mean, 12, 23)}]
	'''.Render(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('Macro and line-break') {
	tR'''
	${=define(`macro1)}${end}
	${=define(`macro2)}Gura${end}
	${=define(`macro3)}
	Gura${end}
	${=define(`macro4)}Gura
	${end}
	${=define(`macro5)}
	Gura
	${end}
	Empty${=call(`macro1)}Here
	NoLineBreak${=call(`macro2)}Here
	NoLineBreak${=call(`macro3)}Here
	LineBreak${=call(`macro4)}Here
	LineBreak${=call(`macro5)}Here
	Macro:${=call(`macro2)}
	Next Line
	'''.Render(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('embed directive') {
	//--------
	tmpl1 = tR'''
	This is a rendering result of tmpl1
	'''
	//--------
	tmpl2 = tR'''
	This is a rendering result of tmpl2
	'''
	//--------
	tmpl3 = tR'''
	This is a rendering result of tmpl3
	'''
	//--------
	tR'''
	embed tmpl1
	${=embed(tmpl1)}
	embed tmpl2
	${=embed(tmpl2)}
	embed tmpl3
	${=embed(tmpl3)}
	'''.Render(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('Local variables in embed') {
	//--------
	tmpl = tR'''
	${x = 'embed', -}
	x = ${x}
	'''
	//--------
	tR'''
	${x = 'body', -}
	x = ${x}
	embed tmpl
	${=embed(tmpl)}
	x = ${x}
	'''.Render(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('Inheritance') {
	//------------------------------------
	// base1 template
	base1 = tR'''
	=== ${=block(`head)}base1-head${end} ===
	<start-of-body>
	${=block(`body)}
	base1-body
	${end}
	<end-of-body>
	'''
	//------------------------------------
	// base2 template
	base2 = tR'''
	${=extends(base1)}
	
	${=block(`head)}base2-head${end}
	
	${=block(`body)}
	${end}
	'''
	//------------------------------------
	// base3 template
	base3 = tR'''
	${=extends(base2)}
	
	${=block(`head)}base3-head${end}
	
	${=block(`body)}
	base3-body
	${end}
	'''
	//------------------------------------
	Println('inherit from base1')
	tR'''
	${=extends(base1)}
	'''.Render(sys.cout)
	//------------------------------------
	Println()
	Println('inherit from base2')
	tR'''
	${=extends(base2)}
	'''.Render(sys.cout)
	//------------------------------------
	Println()
	Println('inherit from base3')
	tR'''
	${=extends(base3)}
	'''.Render(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('Detecting duplicated symbol') {
	tmpl = tR'''
	${=block(`hoge1)}${end}
	${=block(`hoge2)}${end}
	${=block(`hoge3)}${end}
	${=block(`hoge4)}${end}
	${=block(`hoge1)}${end}
	'''
	try {
		tmpl.Render(sys.cout)
	} catch {|e|
		Println('expected error occurs: ', e.text)
	}
}
//-----------------------------------------------------------------------------
TestCase('Embedded string') {
	var = 'hoge'
	Tester(`e'')
	Tester(`e'simple string')
	Tester(`e'variable ${var} reference')
	Tester(`eR'''
	variable ${var} reference
	''')
	// inheritance
	base = T'[${=block(`head)}original${end}] ${=block(`body)}original${end}'
	Tester(`e'${=extends(base)}')
	Tester(`e'${=extends(base)}${=block(`head)}replaced${end}')
	Tester(`e'${=extends(base)}${=block(`body)}replaced${end}')
	Tester(`e'${=extends(base)}${=block(`head)}replaced${end}${=block(`body)}replaced${end}')
	f(a, b) = e'${a} + ${b} = ${a + b}'
	Tester(`f(3, 4))
	Tester(`f(10, 2))
}
//-----------------------------------------------------------------------------
TestCase('Inheritance and loop') {
	// this sample originated from an example in http://jinja.pocoo.org/.
	Users = struct {username:String, url:String}
	users = @(Users) {
		{ 'Iwai Michiko',		'iwai_michiko@example.com'		},
		{ 'Kasai Satoshi',		'kasai_satoshi@example.com'		},
		{ 'Shima Masashi',		'shima_masashi@example.com'		},
		{ 'Suenaga Rena',		'suenaga_rena@example.com'		},
		{ 'Yokokawa Fumiyo',	'yokokawa_fumiyo@example.com'	},
		{ 'Yamanaka Mayuko',	'yamanaka_mayuko@example.com'	},
	}
	layout = tR'''
	<html>
	<body>
	${=block(`body)}${end}
	</body>
	</html>
	'''
	tR'''
	${=extends(layout)}
	${=block(`body)}
	  <ul>
	  ${for (user in users)}
	    <li><a href="${user.url}">${user.username}</a></li>
	  ${end}
	  </ul>
	${end}
	'''.Render(sys.cout)
}
//-----------------------------------------------------------------------------
TestCase('HTML output using inheritance') {
	// this sample originated from an example in http://jinja.pocoo.org/.
	base = tR'''
	<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01//EN">
	<html lang="en">
	<html xmlns="http://www.w3.org/1999/xhtml">
	<head>
	    ${=block(`head)}
	    <link rel="stylesheet" href="style.css" />
	    <title>${=block(`title)}AAAA${end} - My Webpage</title>
	    ${end}
	</head>
	<body>
	    <div id="content">
	    ${=block(`content)}${end}
	    </div>
	    <div id="footer">
	        ${=block(`footer)}
	        &copy; Copyright 2008 by <a href="http://domain.invalid/">you</a>.
	        ${end}
	    </div>
	</body>
	'''
	tR'''
	${=extends(base)}
	${=block(`title)}Index${end}
	${=block(`head)}
	    ${=super(`head)}
	    <style type="text/css">
	        .important { color: #336699; }
	    </style>
	${end}
	${=block(`content)}
	    <h1>Index</h1>
	    <p class="important">
	      Welcome on my awesome homepage.
	    </p>
	${end}
	'''.Render(sys.cout)
}
