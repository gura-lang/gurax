#!/usr/bin/env gurax
import(util.tester) {*}

TestCase('PUnit') {
	exprs = [
		`f()
		`f(1)
		`f(1, 2)
		`f(1, 2, 3, 4, 5)
		`f(1, 2, 'hoge', 4, 'foo')
		`f(1, 2, 3, 4, 5):attr1:attr2
		`f(1, 2) { f1(), f2() }
		`f(a)
		`f(a, b)
		`f(a, b, c, d, e)
		`f(a = 1)
		`f(a = 1, b = 2)
		`f(a = 1, b = 2, c = 3, d = 4, e = 5)
	]
	exprs::PrintPUnitWithLabel()
}

TestCase('Property') {
	Function.PrintPropSlots()
}

TestCase('Default Value (1)') {
	f(a = 'A', b = 'B', c = 'C', d = 'D', e = 'E') = {
		Format('a=%s, b=%s, c=%s, d=%s, e=%s', a, b, c, d, e)
	}
	Tester(`f)
	Tester(`f())
	Tester(`f(a = 'A-mod'))
	Tester(`f(b = 'B-mod'))
	Tester(`f(c = 'C-mod'))
	Tester(`f(d = 'D-mod'))
	Tester(`f(e = 'E-mod'))
	Tester(`f('A-mod', 'B-mod', 'C-mod'))
	Tester(`f(e = 'E-mod', d = 'D-mod', c = 'C-mod', b = 'B-mod', a = 'A-mod'))
}

TestCase('Default Value (2)') {
	f(a = 'A', b = 'B', c, d = 'D', e = 'E') = {
		Format('a=%s, b=%s, c=%s, d=%s, e=%s', a, b, c, d, e)
	}
	Tester(`f)
	Tester(`f())
	Tester(`f(a = 'A-mod', c = 'C'))
	Tester(`f(b = 'B-mod', c = 'C'))
	Tester(`f(c = 'C'))
	Tester(`f(d = 'D-mod', c = 'C'))
	Tester(`f(e = 'E-mod', c = 'C'))
	Tester(`f('A-mod', 'B-mod', 'C-mod'))
	Tester(`f(e = 'E-mod', d = 'D-mod', c = 'C-mod', b = 'B-mod', a = 'A-mod'))
}

TestCase('Optional Argument') {
	f(a?, b?, c?) = {
		Format('a=%s, b=%s, c=%s', cond(a, a, '<Nil>'), cond(b, b, '<Nil>'), cond(c, c, '<Nil>'))
	}
	Tester(`f)
	Tester(`f())
	Tester(`f(1))
	Tester(`f(1, 2))
	Tester(`f(1, 2, 3))
	Tester(`f(a = 1))
	Tester(`f(b = 2))
	Tester(`f(c = 3))
	Tester(`f(a = 1, c = 3))
}

TestCase('Property') {
	func() = {}
	Tester(`func.name)
	Tester(`func.type)
	Tester(`String.Find.type)
	A = class {f():static = {}}
	Tester(`A.f.type)
}

TestCase('return') {
	f() = return
	g() = return(3)
	Tester(`f())
	Tester(`g())
}

TestCase('Recursion') {
	Fact(n as Number) = if (n <= 1) { 1 } else { n * Fact(n - 1) }
	n = 20
	Printf('%d! = %d\n', n, Fact(n))
}

TestCase('nil attribute') {
	f(a as Number) = a
	g(a:nil as Number) = a
	Tester(`f)
	Tester(`g)
	Tester(`f(nil))
	Tester(`g(nil))
}

TestCase('dynamicScope') {
	f_static() = Println('[static]  a = ', a)
	f_dynamic():dynamicScope = Println('[dynamic] a = ', a)
	g_static() {`block} = block.Eval()
	g_dynamic():dynamicScope {`block} = block.Eval()
	a = 'external'
	f_static()
	f_dynamic()
	g_static { Println('[static, block]  a = ', a) }
	g_dynamic { Println('[dynamic, block] a = ', a) }
	scope {
		a = 'local'
		f_static()
		f_dynamic()
		g_static { Println('[static, block]  a = ', a) }
		g_dynamic { Println('[dynamic, block] a = ', a) }
	}
}

TestCase('Named argument') {
	f1(dict%, arg%%) = {
		Println(dict)
		Println(arg)
	}
	f2(a, b, c, dict%, arg%%) = {
		Println(dict)
		Println(arg)
	}
	f1(d1 = 1, d2 = 2, d3 = 3)
	f2(10, 20, 30, d1 = 1, d2 = 2, d3 = 3)
	f2(a = 10, b = 20, c = 30, d1 = 1, d2 = 2, d3 = 3)
	f2(b = 10, c = 20, a = 30, d1 = 1, d2 = 2, d3 = 3)
}

TestCase('Cast of List Value') {
	f(a[] as Number) = Println(a):noMap
	g(a[] as String) = Println(a):noMap
	f([1, 2, 3])
	g([1, 2, 3])
}

TestCase('Quoted Value') {
	f(`x) = {
		Println(x)
		x.PrintPUnit(strIndent = '  ')
	}
	f(3)
	f('str')
	f(v)
	f({v = 2})
	f(g(a, b, c))
}

TestCase('block') {
	f() {block} = block
	g() {`block} = block
	Tester(`f)
	Tester(`g)
	Tester(`f{}.type)
	Tester(`g{}.type)
	Tester(`f {|v1, v2| func(a, b, c) })
	Tester(`g {|v1, v2| func(a, b, c) })
}

TestCase('Property') {
	Fact(n as Number) = if (n <= 1) { 1 } else { n * Fact(n - 1) }
	Tester(`Fact.expr)
	Tester(`Fact.name)
	Tester(`Fact.type)
}

TestCase('Function()') {
	g(v*) = {'[' + v.Join(',') + ']'}
	Tester(`{f = Function {}, f})
	Tester(`{f = Function {}, f()})
	Tester(`{f = Function {g()}, f()})
	Tester(`{f = Function(v) {}, f})
	Tester(`{f = Function(v) {}, f(3)})
	Tester(`{f = Function(v) {g(v)}, f(3)})
	Tester(`{f = Function(v) {}, f()})
	Tester(`{f = Function(v[]) {}, f})
	Tester(`{f = Function(v[]) {}, f([1, 2, 3])})
	Tester(`{f = Function(v[]) {}, f()})
	Tester(`{f = Function(v1, v2, v3, v4, v5, v6) {}, f})
	Tester(`{f = Function(v1, v2, v3, v4, v5, v6) {}, f(1, 2, 3, 4, 5, 6)})
	Tester(`{f = Function(v1, v2, v3, v4, v5, v6) {g(v1, v2, v3, v4, v5, v6)}, f(1, 2, 3, 4, 5, 6)})
	Tester(`{f = Function(v1, v2, v3, v4, v5, v6) {}, f(3)})
}

TestCase('Function Creation by &{}') {
	g(b, c, d) = b + c + d
	Tester(`{f = &{}, f})
	Tester(`{f = &{}, f()})
	Tester(`{f = &{}, f(3)})
	Tester(`{f = &{$a}, f})
	Tester(`{f = &{$a}, f()})
	Tester(`{f = &{$a}, f(3)})
	Tester(`{f = &{$a}, f(3, 4)})
	Tester(`{f = &{$a + $b}, f})
	Tester(`{f = &{$a + $b}, f(3, 4)})
	Tester(`{f = &{$a + $b + $c + $d + $e}, f})
	Tester(`{f = &{$a + $b + $c + $d + $e}, f(3, 4, 5, 6, 7)})
	Tester(`{f = &{$a + $b * $c}, f})
	Tester(`{f = &{$a + $b * $c}, f(3, 4, 5)})
	Tester(`{f = &{$a * $b + $c}, f})
	Tester(`{f = &{$a * $b + $c}, f(3, 4, 5)})
	Tester(`{f = &{g($a, $b, $c) + $d}, f})
	Tester(`{f = &{g($a, $b, $c) + $d}, f(3, 4, 5, 6)})
	Tester(`{f = &{$a + g($b, $c, $d)}, f})
	Tester(`{f = &{$a + g($b, $c, $d)}, f(3, 4, 5, 6)})
	Tester(`{f = &{$a + $a}, f})
	Tester(`{f = &{$a + $a}, f(3)})
	Tester(`{f = &{|a|}, f})
	Tester(`{f = &{|a| $x}, f})
	Tester(`{f = &{|a|}, f()})
	Tester(`{f = &{|a|}, f(3)})
	Tester(`{f = &{|a|}, f(3, 4)})
	Tester(`{f = &{|a, b| a + b}, f})
	Tester(`{f = &{|a, b| a + b}, f(3, 4)})
	Tester(`{f = &{|a as String, b as Number| a * b}, f('hello', 4)})
}

TestCase('Variable Expansion by Asterisk Operator') {
	f1(a, b*) = {
		Format('a=%d b=[%s]', a, b.Join(','))
	}
	f2(a, b, c, d, e*) = {
		Format('a=%d b=%d c=%d d=%d e=[%s]', a, b, c, d, e.Join(','))
	}
	f3(a, b, c, d, e) = {
		Format('a=%d b=%d c=%d d=%d e=%s', a, b, c, d, e)
	}
	f4(a*) = {
		Format('a=[%s]', a.Join(','))
	}
	f5(a, b, `c) = {
		Format('a=%d b=%s c=%s', a, b, c)
	}
	Tester(`f1)
	Tester(`f2)
	Tester(`f3)
	Tester(`f4)
	x = []
	Tester(`f1(x*))
	Tester(`f2(x*))
	Tester(`f3(x*))
	Tester(`f4(x*))
	x = [1, 2, 3, 4]
	Tester(`f1(x*))
	Tester(`f2(x*))
	Tester(`f3(x*))
	Tester(`f4(x*))
	x = [1, 2, 3, 4, 5]
	Tester(`f1(x*))
	Tester(`f2(x*))
	Tester(`f3(x*))
	Tester(`f4(x*))
	x = [1, 2, 3, 4, 5, 6]
	Tester(`f1(x*))
	Tester(`f2(x*))
	Tester(`f3(x*))
	Tester(`f4(x*))
	x = [1, 2, 3, 4, 5, 6, 7, 8, 9, 10]
	Tester(`f1(x*))
	Tester(`f2(x*))
	Tester(`f3(x*))
	Tester(`f4(x*))
	Tester(`f5([1, 2]*, 3))
	Tester(`f5([1, 2, 3]*))
	Tester(`f1(x.Each()*))
	Tester(`f2(x.Each()*))
	Tester(`f3(x.Each()*))
	Tester(`f4(x.Each()*))
	x = ()
	Tester(`f1(x*))
	Tester(`f2(x*))
	Tester(`f3(x*))
	Tester(`f4(x*))
	x = (1, 2, 3, 4)
	Tester(`f1(x*))
	Tester(`f2(x*))
	Tester(`f3(x*))
	Tester(`f4(x*))
	x = (1, 2, 3, 4, 5)
	Tester(`f1(x*))
	Tester(`f2(x*))
	Tester(`f3(x*))
	Tester(`f4(x*))
	x = (1, 2, 3, 4, 5, 6)
	Tester(`f1(x*))
	Tester(`f2(x*))
	Tester(`f3(x*))
	Tester(`f4(x*))
	x = (1, 2, 3, 4, 5, 6, 7, 8, 9, 10)
	Tester(`f1(x*))
	Tester(`f2(x*))
	Tester(`f3(x*))
	Tester(`f4(x*))
}

TestCase('Block delegation') {
	f() {block?} = { if (block) {block(block.type)} else {'(no block)'} }
	g1() {`block?} = { f {{block}} }
	g2() {block} = { f {{block}} }
	g3() = { block = `(a = 3), f {{block}} }
	Tester(`g1 {|str| Format('type = %s', str) })
	Tester(`g1())
	Tester(`g2 {|str| Format('type = %s', str) })
	Tester(`g3())
}

TestCase('Attribute :void') {
	f():void = 'hello'
	Println(f().__str__():nilVisible)
}

TestCase('Scope for block') {
	f() {block} = {
		block()
	}
	g() = {
		x = 'OK'
		f { Println(x) }
	}
	g()
}

TestCase('noCast') {
	f(a as String) = { 'OK' }
	g(a:noCast as String) = { 'NG' }
	Tester(`f(3))
	Tester(`g(3))
}

TestCase('Operator *') {
	Tester(`(Format * 'Name'))
	Tester(`(Format * ('Name:%s Age:%d', 'hoge', 23)))
	Tester(`(math.Sqrt * 2))
	Tester(`(math.Sqrt * (2,)))
	Tester(`(math.Atan2 * (2, 3)))
	Tester(`(Color * 'black'))
	Tester(`(Color * ('black', 64)))
	Tester(`('abcdefghijklmnopqrstuvwxyz'.Left * 8))
	Tester(`('abcdefghijklmnopqrstuvwxyz'.Mid * 8))
	Tester(`('abcdefghijklmnopqrstuvwxyz'.Mid * (8, 5)))
	Tester(`(Color * ['black', 'red', 'green', ('blue', 64)]))
	Person = struct { name as String, age as Number, mobile as String }
	p = Person * ('hoge', 23, 'xxx-yyyy-zzzz')
	Tester(`Format('%s %d %s', p.name, p.age, p.mobile))
}

TestCase('stringCast attribute') {
	f(a as Number) = a
	g(a:stringCast as Number) = a
	Tester(`f('1234'))
	Tester(`g('1234'))
}

TestCase('Tuple Casting') {
	Person = struct {
		name as String
		email as String
		age as Number
		birthDay as String
		mobile as String
	}
	f(person as Person):map = {
		Printf('%s, %s, %d, %s, %s\n', person.name, person.email, person.age, person.email, person.birthDay, person.mobile)
	}
	g(value):map = {
		Println(value)
	}
	f(Person('Hayasaka Hiroko','hayasaka_hiroko@example.com',33,'1976/8/27','090-9134-30BF'))
	f(('Hayasaka Hiroko','hayasaka_hiroko@example.com',33,'1976/8/27','090-9134-30BF'))
	f([
		('Hayasaka Hiroko','hayasaka_hiroko@example.com',33,'1976/8/27','090-9134-30BF')
		('Hannda Yumiko','hannda_yumiko@example.com',75,'1934/10/20','080-6713-87BI')
		('Honma Chise','honma_chise@example.com',46,'1964/3/9','090-5237-82FJ')
		('Kawahata Nana','kawahata_nana@example.com',47,'1962/4/23','090-4760-51JA')
	])
	g(Person('Hayasaka Hiroko','hayasaka_hiroko@example.com',33,'1976/8/27','090-9134-30BF'))
	g(('Hayasaka Hiroko','hayasaka_hiroko@example.com',33,'1976/8/27','090-9134-30BF'))
}

