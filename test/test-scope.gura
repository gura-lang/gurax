#!/usr/bin/env gurax
import(util.tester) {*}

TestCase('Explicit creation of scope') {
	a = 3
	scope {
		a = 5
		Println('a = ', a)
	}
	Println('a = ', a)
}

TestCase('List#Each()\'s scope') {
	a = 5
	b = 3
	Printf('#1 a = %d, b = %d\n', a, b)
	[1..1].Each {|b|
		Printf('#2 a = %d, b = %d\n', a, b)
		[2..2].Each {|b|
			Printf('#3 a = %d, b = %d\n', a, b)
			a = -5
			b = -3
			Printf('#4 a = %d, b = %d\n', a, b)
		}
		Printf('#5 a = %d, b = %d\n', a, b)
	}
	Printf('#6 a = %d, b = %d\n', a, b)
}

TestCase('Test of module scope') {
	Tester(`path.Println)
}

TestCase('extern attribute') {
	f1() = { var:extern = 'modified' }
	f2() = { var, var = 'modified' }
	f3() = { var = 'modified' }
	f4() = { varErr:extern = 'modified' }
	var = 'original', Tester(`f1()), Println('var = ', var)
	var = 'original', Tester(`f2()), Println('var = ', var)
	var = 'original', Tester(`f3()), Println('var = ', var)
	var = 'original', Tester(`f4()), Println('var = ', var)
}

TestCase('Outer Scope') {
	a = [1, 2, 3, 4]
	scope {
		a = [5, 6, 7, 8, 9, 10]
		Printf('a=%s, a.len=%d\n', a, a.len):noMap
		Printf('^a=%s, (^a).len=%d\n', ^a, (^a).len):noMap
		(^a)[0] = 9
	}
	Printf('a=%s, a.len=%d\n', a, a.len):noMap
}
