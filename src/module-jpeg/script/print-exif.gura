#!/usr/bin/env gurax
import(jpeg)
import(argopt)

PrintIFD(ifd:jpeg.IFD, indentLevel:Number = 0) = {
	ifd.tags.Each {|tag|
		if (tag.offset == tag.offsetToValue) {
			Printf('%*s%04x:%s(%04x) [%s]: %s\n',
				indentLevel * 2, '', tag.offset, tag.name, tag.tagId,
				tag.type.name, tag.value)
		} else {
			Printf('%*s%04x:%s(%04x) [%s,%04x]: %s\n',
				indentLevel * 2, '', tag.offset, tag.name, tag.tagId,
				tag.type.name, tag.offsetToValue, tag.value)
		}
		if (tag.type == `IFD) {
			PrintIFD(tag.value, indentLevel + 1)
		}
	}
}

argopt.Parser {|p|
	errorFlag = false
	try { [argv, opt] = p.Parse(sys.argv) } catch { errorFlag = true }
	if (errorFlag || argv.len < 1) {
		sys.cerr.Println('usage: print-exif jpeg-file')
		sys.Exit(1)
	}
}
fileName = argv[0]
c = jpeg.Content(fileName)
Println('==== IFD0 ====')
PrintIFD(c.exif.ifd0)
Println('==== IFD1 ====')
PrintIFD(c.exif.ifd1)
