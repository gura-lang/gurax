#!/usr/bin/env gurax
import(ml.mnist)
import(ml.qmnist)
import(ml.kmnist)
import(recog)
import(conio.progress)

if (recog.dbName == 'mnist') {
	db = ml.mnist.Database('.')
} elsif (recog.dbName == 'qmnist') {
	db = ml.qmnist.Database('.')
} elsif (recog.dbName == 'kmnist') {
	db = ml.kmnist.Database('.')
} else {
	sys.cerr.Printf('unknown database: %s', recog.dbName)
	sys.Exit()
}
nCorrects = 0
nSamples = db.test.nSamples
Open('eval.result.txt', 'w') {|out|
	sys.cerr.Printf('Test with %d samples in %s\n', nSamples, recog.dbName)
	conio.progress.ProgressBar('', 20, nSamples) {|progressBar|
		db.test.Each(`float) {|sample, iSample|
			result = recog.Eval(sample.input)
			progressBar.SetValue(iSample)
			[likelihood, labelRecog] = result.FindMax(-1, 0):pair
			if (labelRecog == sample.label) {
				out.Printf(' "%d" .. correct with likelihood %.1f%%\n', labelRecog, likelihood * 100)
				nCorrects += 1
			} else {
				out.Printf(' "%d" .. wrong. correct is "%d"\n', labelRecog, sample.label)
				ml.mnist.PrintImage(sample.input, out)
			}
		}
		progressBar.Clear()
	}
	sys.cerr.Printf('accuracy: %d/%d (%.1f%%)\n', nCorrects, nSamples, nCorrects / nSamples * 100)
}
