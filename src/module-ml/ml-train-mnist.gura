#!/usr/bin/env gurax
import(ml.mnist)
import(ml.qmnist)

nTraining = 10
db = ml.mnist.Database('../../sample/resource/mnist')
//db = ml.qmnist.Database('../../sample/resource/qmnist')
if (true) {
	model = `((image |.| affineWeight + affineBias) |*| ml.Softmax())
	shapeImage = (1, db.nRows * db.nCols)
	affineWeight = @float(shapeImage[-1], db.nClasses)
	affineWeight.Inject(Random.NormalSeq(0, ml.Xavier(affineWeight.shape[0])))
	affineBias = @float(1, affineWeight.shape[-1])
	affineBias.Inject(Random.NormalSeq(0, ml.Xavier(affineBias.shape[-1])))
} elsif (false) {
	model = `{
		t1 = (image |.| affineWeight1 + affineBias1) |*| ml.ReLU()
		t2 = t1 |.| affineWeight2 + affineBias2
		t3 = t2 |*| ml.Softmax()
	}
	shapeImage = (1, db.nRows * db.nCols)
	affineWeight1 = @float(shapeImage[-1], 100)
	affineWeight1.Inject(Random.NormalSeq(0, ml.Xavier(affineWeight1.shape[0])))
	affineBias1 = @float(1, affineWeight1.shape[-1])
	affineBias1.Inject(Random.NormalSeq(0, ml.Xavier(affineBias1.shape[-1])))
	affineWeight2 = @float(affineWeight1.shape[-1], db.nClasses)
	affineWeight2.Inject(Random.NormalSeq(0, ml.Xavier(affineWeight2.shape[0])))
	affineBias2 = @float(1, affineWeight2.shape[-1])
	affineBias2.Inject(Random.NormalSeq(0, ml.Xavier(affineBias2.shape[-1])))
} else {
	model = `{
		t1 = (image |*| conv1 + convBias1) |*| ml.ReLU() |*| pool1 |*| flatten1
		t2 = (t1 |.| affineWeight1 + affineBias1) |*| ml.ReLU()
		t3 = (t2 |.| affineWeight2 + affineBias2)
		t4 = t3 |*| ml.Softmax()
	}
	shapeImage = (1, 1, db.nRows, db.nCols)
	convFilter1 = @float(30, 1, 5, 5) // 30 filters, 1 x 5 x 5
	convFilter1.Inject(Random.NormalSeq(0, ml.Xavier(convFilter1.shape[0])))
	conv1 = ml.Conv2d(convFilter1, stride = 1, padding = 0)
	[nRowsConv1, nColsConv1] = conv1.CalcSizeOut(db.nRows, db.nCols)
	convBias1 = @float(1, convFilter1.shape[0], nRowsConv1, nColsConv1)
	convBias1.Inject(Random.NormalSeq(0, ml.Xavier(convBias1.shape[-1])))
	pool1 = ml.MaxPool2d(2, 2, stride = 2)
	[nRowsPool1, nColsPool1] = pool1.CalcSizeOut(nRowsConv1, nColsConv1)
	flatten1 = ml.Reshape(1, convFilter1.shape[0] * nRowsPool1 * nColsPool1)
	affineWeight1 = @float(convFilter1.shape[0] * nRowsPool1 * nColsPool1, 100)
	affineWeight1.Inject(Random.NormalSeq(0, ml.Xavier(affineWeight1.shape[0])))
	affineBias1 = @float(1, affineWeight1.shape[-1])
	affineBias1.Inject(Random.NormalSeq(0, ml.Xavier(affineBias1.shape[-1])))
	affineWeight2 = @float(affineWeight1.shape[-1], db.nClasses)
	affineWeight2.Inject(Random.NormalSeq(0, ml.Xavier(affineWeight2.shape[0])))
	affineBias2 = @float(1, affineWeight2.shape[-1])
	affineBias2.Inject(Random.NormalSeq(0, ml.Xavier(affineBias2.shape[-1])))
}
scope {
	arrayImage = db.train.imageSet.ToArray(`float, flatten = true, numMax = 1)	// (nSamples, db.nRows * db.nCols)
	arrayLabel = db.train.labelSet.ToArray(`float, oneHot = true)				// (nSamples, db.nClasses)
	optimizer = ml.Optimizer.GradientDescent(0.01)
	//nSamples = arrayImage.shape[0]
	//nSamples = 10000
	nSamples = 1000
	Printf('Training with %d samples [Image Size: %dx%d, Label Size: %d]\n', nSamples, db.nRows, db.nCols, db.nClasses)
	ml.Trainer(model, optimizer, `image) {|trainer|
		repeat (nTraining) {|iRepeat|
			repeat (nSamples) {|i|
				image = arrayImage[i].Reshape(shapeImage*)
				correct = arrayLabel[i].Reshape(1, nil)
				trainer.Train(correct, image)
			}
			Printf('#%d error=%g\n', iRepeat + 1, trainer.CalcMeanSquaredError(correct))
		}
	}
}
pattern = [' ', '.', ':', '#', '#']
scope {
	// evaluation
	arrayImage = db.test.imageSet.ToArray(`float, flatten = true, numMax = 1)	// (nSamples, db.nRows * db.nCols)
	arrayLabel = db.test.labelSet.ToArray(`float, oneHot = true)				// (nSamples, db.nClasses)
	nCorrects = 0
	nSamples = arrayImage.shape[0]
	Printf('Test with %d samples\n', nSamples)
	repeat (nSamples) {|i|
		image = arrayImage[i].Reshape(shapeImage*)
		t = arrayLabel[i].Reshape(1, nil)
		y = model.Eval()
		idxResult = y.ArgMax(-1, 0)
		idxCorrect = t.ArgMax(-1, 0)
		if (idxResult == idxCorrect) {
			//Printf(' "%d" .. correct with likelihood %.1f%%\n', idxResult, y[0, idxResult] * 100)
			nCorrects += 1
		} else {
			//arrayImage[i].ToList().Fold(db.test.imageSet.nCols) {|row| Printf('%s\n', pattern.Get(row * 4).Join())}
			//Printf(' "%d" .. wrong. correct is "%d"\n', idxResult, idxCorrect)
		}
	}
	Printf('accuracy: %d/%d (%.1f%%)\n', nCorrects, nSamples, nCorrects / nSamples * 100)
}
