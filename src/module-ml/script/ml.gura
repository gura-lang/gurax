#!/usr/bin/env gura
import(gzip)
import(ml):binary {*}
import(conio)

KeyAbort(msg? as String, keyCodes* as Number) = {
	if (conio.ReadKey() in keyCodes) {
		conio.Clear(`line)
		Print(msg || 'Are you sure to abort? [y/N]')
		key = conio.WaitKey()
		Println()
		return(key == Ord('y'))
	}
	return(false)
}

Trainer.DoTraining(iterator as Iterator, nSamplesWhole as Number, nEpochs as Number, batchSize as Number, nIterations? as Number, moduleNameProduct as String = 'recog') = {
	nSamples = cond(!nIterations || nSamplesWhole < batchSize * nIterations, nSamplesWhole, batchSize * nIterations)
	Printf('Training with %d samples -- Press Q to abort the process.\n', nSamples)
	abortFlag = false
	repeat (nEpochs) {|iEpoch|
		conio.progress.ProgressBar(20, nSamples, 'Epoch%d ', iEpoch + 1) {|progressBar|
			iterator.Head(nIterations) {|sample, iIteration|
				result = sample.result
				this.Train(result, sample.input)
				if (progressBar.SetValue(iIteration * batchSize) && KeyAbort(nil, Ord('q'))) {
					abortFlag = true
					break
				}
			}
			progressBar.Clear()
		}
		Printf('Epoch%d error=%g\n', iEpoch + 1, this.CalcMeanSquaredError(result))
		abortFlag && break
	}
	this.CreateModule(moduleNameProduct)
}

Trainer.CreateModule(moduleName as String, variables* as Expr):dynamicScope = {
	fileName = moduleName + '.gura'
	out = Open(fileName, 'w')
	tR'''
	#!/usr/bin/env gurax
	import(ml)
	import(gzip)

	timeStamp = '${DateTime.Now().__str__()}'

	Eval(${(this.EachInputSymbol() + ' as Array').Join(', ')}) = {
		${if (this.model.type == `Block)}
		${this.model.EachElem():*__str__() + '\n'}
		${else}
		${this.model}
		${end}
	}
	'''.Render(out)
	variables.Each {|variable|
		tR"""
		
		${variable} = b'''
		""".Render(out)
		out.Writer@base64().Writer@gzip().Serialize(variable.Eval())
		tR"""
		'''base64.Reader().Reader@gzip().Deserialize()
		""".Render(out)
	}
	this.EachNode(`variable, `gear) {|node|
		(node.expr.type != `Identifier) && continue
		tR"""
		
		${node.expr.symbol} = b'''
		""".Render(out)
		if (node.type == `variable) {
				out.Writer@base64().Writer@gzip().Serialize(node.output)
		} else { // node.type == `gear
			out.Writer@base64().Writer@gzip().Serialize(node.gear)
		}
		tR"""
		'''base64.Reader().Reader@gzip().Deserialize()
		""".Render(out)
	}
	out = nil
	sys.cerr.Printf('%s was created\n', fileName)
}
