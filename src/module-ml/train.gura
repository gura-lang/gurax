#!/usr/bin/env gurax
import(ml)
import(ml.mnist)
import(gzip)
import(util.tester) {*}

db = ml.mnist.Database('../../sample/resource/mnist')

if (false) {
	model = `(image |*| linear |*| ml.Softmax())
	shapeImage = (1, db.nRows * db.nCols)
	linear = ml.Linear(db.nClasses)
} elsif (false) {
	model = `(image |*| linear1 |*| ml.ReLU() |*| linear2 |*| ml.Softmax())
	shapeImage = (1, db.nRows * db.nCols)
	linear1 = ml.Linear(100)
	linear2 = ml.Linear(db.nClasses)
} else {
	model = `{
		t1 = image |*| conv1 |*| ml.ReLU() |*| pool1 |*| flatten1
		t2 = t1 |*| linear1 |*| ml.ReLU()
		t3 = t2 |*| linear2
		t4 = t3 |*| ml.Softmax()
	}
	shapeImage = (1, 1, db.nRows, db.nCols)
	conv1 = ml.Conv2d(nFilters = 30, nRowsFilter = 5, nColsFilter = 5, stride = 1, padding = 0)
	pool1 = ml.MaxPool2d(nRowsKernel = 2, nColsKernel = 2, stride = 2)
	flatten1 = ml.Reshape(1, nil)
	linear1 = ml.Linear(100)
	linear2 = ml.Linear(db.nClasses)
}

scope {
	nTraining = 10
	arrayImage = db.train.imageSet.ToArray(`float, flatten = true, numCeil = 1)	// (nSamples, db.nRows * db.nCols)
	arrayLabel = db.train.labelSet.ToArray(`float, oneHot = true)				// (nSamples, db.nClasses)
	optimizer = ml.Optimizer.GradientDescent(0.01)
	//nSamples = arrayImage.nRows
	//nSamples = 10000
	nSamples = 1000
	Printf('Training with %d samples [Image Size:%dx%d, Label Size:%d]\n', nSamples, db.nRows, db.nCols, db.nClasses)
	ml.Trainer(model, optimizer, `image) {|trainer|
		trainer.random.seed = 0x12345678
		repeat (nTraining) {|iRepeat|
			repeat (nSamples) {|i|
				image = arrayImage[i].Reshape(shapeImage*)
				correct = arrayLabel[i].Reshape(1, nil)
				trainer.Train(correct, image)
			}
			Printf('#%d error=%g\n', iRepeat + 1, trainer.CalcMeanSquaredError(correct))
		}
		trainer.SaveAsModule('recog_mnist', `shapeImage)
	}
}
