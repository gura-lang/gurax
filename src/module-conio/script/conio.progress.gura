#!/usr/bin/env gurax
import(conio)

ProgressBar = class {
	nLightsMax as Number
	valueMax as Number
	nLights as Number = -1
	nColsValue as Number
	strHead as String
	__init__(nLightsMax as Number, valueMax as Number, formatHead as String = '', args*) = {
		this.nLightsMax = nLightsMax
		this.valueMax = valueMax
		this.nColsValue = Format('%d', valueMax).len
		this.strHead = Format(formatHead, args*)
	}
	Reset() = {
		this.nLights = -1
	}
	SetValue(value as Number) = {
		nLightsCur = Int(this.nLightsMax * value / this.valueMax)
		(this.nLights == nLightsCur) && return(false)
		Printf('%s%s%s %*d/%d\r', this.strHead,
				'#' * nLightsCur, '-' * (this.nLightsMax - nLightsCur),
				this.nColsValue, value, this.valueMax)
		this.nLights = nLightsCur
		true
	}
	Clear() = conio.Clear(`line)
}

if (__name__ == '__main__') {
	ProgressBar(20, 10) {|progressBar|
		repeat (progressBar.valueMax + 1) {|value|
			progressBar.SetValue(value)
			os.Sleep(.1)
		}
		progressBar.Clear()
	}
	ProgressBar(20, 20) {|progressBar|
		repeat (progressBar.valueMax + 1) {|value|
			progressBar.SetValue(value)
			os.Sleep(.1)
		}
		progressBar.Clear()
	}
	ProgressBar(40, 10) {|progressBar|
		repeat (progressBar.valueMax + 1) {|value|
			progressBar.SetValue(value)
			os.Sleep(.1)
		}
		progressBar.Clear()
	}
	ProgressBar(40, 20) {|progressBar|
		repeat (progressBar.valueMax + 1) {|value|
			progressBar.SetValue(value)
			os.Sleep(.1)
		}
		progressBar.Clear()
	}
	ProgressBar(20, 20, 'Phase#%d,%d ', 3, 2) {|progressBar|
		repeat (progressBar.valueMax + 1) {|value|
			progressBar.SetValue(value)
			os.Sleep(.1)
		}
		Println()
	}
}
