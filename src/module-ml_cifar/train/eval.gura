#!/usr/bin/env gurax
import(recog)
import(conio.progress)
cifar = import(ml.cifar10)

db = cifar.Database('..')
nCorrects = 0
nSamples = db.test.nSamples
Open('eval.result.txt', 'w') {|out|
	sys.cerr.Printf('Test with %d samples\n', nSamples)
	conio.progress.ProgressBar(20, nSamples) {|progressBar|
		db.test.Each(`float) {|sample, iSample|
			result = recog.Eval(sample.input)
			progressBar.SetValue(iSample)
			[likelihood, labelRecog] = result.FindMax(-1, 0):pair
			if (labelRecog == sample.label) {
				out.Printf(' "%s" .. correct with likelihood %.1f%%\n', db.labelNames[labelRecog], likelihood * 100)
				nCorrects += 1
			} else {
				out.Printf(' "%s" .. wrong. correct is "%s"\n', db.labelNames[labelRecog], db.labelNames[sample.label])
				cifar.PrintImage(sample.input, out)
			}
		}
		progressBar.Clear()
	}
	sys.cerr.Printf('accuracy: %d/%d (%.1f%%)\n', nCorrects, nSamples, nCorrects / nSamples * 100)
}
