#!./gurax
import(util)

util.TestCase('Recursion') {
	Fact(n:Number) = if (n <= 1) { 1 } else { n * Fact(n - 1) }
	n = 20
	Printf('%d! = %d\n', n, Fact(n))
}

util.TestCase('dynamic_scope') {
	f_static() = Println('[static]  a = ', a)
	f_dynamic():dynamic_scope = Println('[dynamic] a = ', a)
	g_static() {`block} = block.Eval()
	g_dynamic():dynamic_scope {`block} = block.Eval()
	a = 'external'
	f_static()
	f_dynamic()
	g_static { Println('[static, block]  a = ', a) }
	g_dynamic { Println('[dynamic, block] a = ', a) }
	scope {
		a = 'local'
		f_static()
		f_dynamic()
		g_static { Println('[static, block]  a = ', a) }
		g_dynamic { Println('[dynamic, block] a = ', a) }
	}
}

util.TestCase('Named argument') {
	f1(dict%, arg%%) = {
		Println(dict)
		Println(arg)
	}
	f2(a, b, c, dict%, arg%%) = {
		Println(dict)
		Println(arg)
	}
	f1(d1 => 1, d2 => 2, d3 => 3)
	f2(10, 20, 30, d1 => 1, d2 => 2, d3 => 3)
	f2(a => 10, b => 20, c => 30, d1 => 1, d2 => 2, d3 => 3)
	f2(b => 10, c => 20, a => 30, d1 => 1, d2 => 2, d3 => 3)
}

util.TestCase('Cast of list value') {
	f(a[]:Number) = Println(a):nomap
	g(a[]:String) = Println(a):nomap
	f([1, 2, 3])
	g([1, 2, 3])
}

