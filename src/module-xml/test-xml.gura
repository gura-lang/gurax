#!/usr/bin/env gurax
//==============================================================================
// test-xml.gura
//==============================================================================
import(util.tester) {*}
import(xml)

sampleText1 = R'''
<?xml version="1.0"?>
<PurchaseOrder PurchaseOrderNumber="99503" OrderDate="1999-10-20">
  <!-- Address #1 -->
  <Address Type="Shipping">
    <Name>Ellen Adams</Name>
    <Street>123 Maple Street</Street>
    <City>Mill Valley</City>
    <State>CA</State>
    <Zip>10999</Zip>
    <Country>USA</Country>
  </Address>
  <!-- Address #2 -->
  <Address Type="Billing">
    <Name>Tai Yee</Name>
    <Street>8 Oak Avenue</Street>
    <City>Old Town</City>
    <State>PA</State>
    <Zip>95819</Zip>
    <Country>USA</Country>
  </Address>
  <DeliveryNotes>Please leave packages in shed by driveway.</DeliveryNotes>
  <Items>
    <Item PartNumber="872-AA">
      <ProductName>Lawnmower</ProductName>
      <Quantity>1</Quantity>
      <USPrice>148.95</USPrice>
      <Comment>Confirm this is electric</Comment>
    </Item>
    <Item PartNumber="926-AA">
      <ProductName>Baby Monitor</ProductName>
      <Quantity>2</Quantity>
      <USPrice>39.98</USPrice>
      <ShipDate>1999-05-21</ShipDate>
    </Item>
  </Items>
</PurchaseOrder>
'''

sampleText2 = R'''
<?xml version="1.0" encoding="UTF-8"?>
<SoftwareEngineer>
<empl id="01">
<name>
<projectname> Man-router</projectname>
<Workingdomain> machine learning</Workingdomain>
</name>
<Enddate>
<entities><![CDATA[
This is the local project with the fibre optics.
All the statistical manipulation is performed. Example. '"&<> and submission date 12/12/2020
]]></entities>
</Enddate>
</empl>
</SoftwareEngineer>
'''

ParseAndCompose(text as String) = {
	doc = xml.Parse(text)
	doc.Compose()
  Println()
  doc.root.Walk {|node| Printf('%*s%s\n', node.depth * 2, '', node)}
}

Println('----------------------------------------------------------------')
ParseAndCompose(sampleText1)

Println('----------------------------------------------------------------')
ParseAndCompose(sampleText2)

TestCase('Node#parent') {
  doc = xml.Parse(sampleText1)
  doc.root.Walk {|node|
    Printf('%*s%s%s -> %s\n', node.depth * 2, '', node.type,
          cond(node.IsElement(), '(' + node.name + ')', ''),
          cond(node.parent, 'element(' + node.parent.name + ')', 'nil'))
  }
}

TestCase('Element#Each()') {
  doc = xml.Parse(sampleText1)
  Println(doc.root.Each())
}

TestCase('Element#EachText()') {
  doc = xml.Parse(sampleText1)
  Println(doc.root.EachText())
}

TestCase('Element#EachElement()') {
  doc = xml.Parse(sampleText1)
  Println('EachElement()')
  Println(doc.root.EachElement())
  Println("EachElement('Address')")
  Println(doc.root.EachElement('Address'))
}

TestCase('Element#GetAttr()') {
  elem = xml.Parse(sampleText1).root
  Tester(`elem.GetAttr('PurchaseOrderNumber'))
  Tester(`elem.GetAttr('OrderDate'))
}

TestCase('Element#GetAttrName()') {
  elem = xml.Parse(sampleText1).root
  Tester(`elem.GetAttrName(0))
  Tester(`elem.GetAttrName(1))
  Tester(`elem.GetAttrName('PurchaseOrderNumber'))
  Tester(`elem.GetAttrName('OrderDate'))
}

TestCase('Element#GetAttrValue()') {
  elem = xml.Parse(sampleText1).root
  Tester(`elem.GetAttrValue(0))
  Tester(`elem.GetAttrValue(1))
  Tester(`elem.GetAttrValue('PurchaseOrderNumber'))
  Tester(`elem.GetAttrValue('OrderDate'))
}

TestCase('Element#attrs') {
  elem = xml.Parse(sampleText1).root
  Tester(`elem.attrs['PurchaseOrderNumber'])
  Tester(`elem.attrs['OrderDate'])
}

TestCase('XmlDecl') {
    PrintXmlDecl(xmlDecl as xml.XmlDecl) = {
      Printf('version=%s encoding=%s standalone=%s\n', xmlDecl.version, xmlDecl.encoding, xmlDecl.standalone)
    }
    xml.Parse('<?xml version="1.0"?>\n<root />\n') {|doc| PrintXmlDecl(doc.xmlDecl)}
    xml.Parse('<?xml version="1.0" encoding="utf-8"?>\n<root />\n') {|doc| PrintXmlDecl(doc.xmlDecl)}
    xml.Parse('<?xml version="1.0" encoding="utf-8" standalone="yes"?>\n<root />\n') {|doc| PrintXmlDecl(doc.xmlDecl)}
    xml.Parse('<?xml version="1.0" encoding="utf-8" standalone="no"?>\n<root />\n') {|doc| PrintXmlDecl(doc.xmlDecl)}
}

TestCase('Element#Walk()') {
  doc = xml.Parse(sampleText1)
  Println('Walk()')
  doc.root.Walk {|node| Printf('%*s%s\n', node.depth * 2, '', node)}
  Println()
  Println('Walk():element')
  doc.root.Walk():element {|node| Printf('%*s%s\n', node.depth * 2, '', node)}
  Println()
  Println('Walk():text')
  doc.root.Walk():text {|node| Printf('%*s%s\n', node.depth * 2, '', node)}
  Println()
  Println('Walk():comment')
  doc.root.Walk():comment {|node| Printf('%*s%s\n', node.depth * 2, '', node)}
  Println()
  Println('Walk():text:comment')
  doc.root.Walk():text:comment {|node| Printf('%*s%s\n', node.depth * 2, '', node)}
}
