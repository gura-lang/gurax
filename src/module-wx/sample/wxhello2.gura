#!/usr/bin/env gurax
// wxWidgets "Hello world" Program
import(wx)

MyApp = class(wx.App) {
	OnInit() = {
		frame = MyFrame('Hello World', wx.Point(50, 50), wx.Size(450, 340))
		frame.Show(true)
		true
	}
}

MyFrame = class(wx.Frame) {
	__init__(title as String, pos as wx.Point, size as wx.Size) = {
			|nil, wx.ID_ANY, title, pos, size|
		wx.MenuBar {|menuBar|
			menuBar.Append(
				wx.Menu {|menu|
					menu.Append(ID_Hello, '&Hello...\tCtrl-H',
									'Help string shown in status bar for this menu item')
					menu.AppendSeparator()
					menu.Append(wx.ID_EXIT)
					menu
				}, '&File')
			menuBar.Append(
				wx.Menu {|menu|
					menu.Append(wx.ID_ABOUT)
					menu
				}, '&Help')
			this.SetMenuBar(menuBar)
		}
		this.CreateStatusBar()
		this.SetStatusText('Welcome to wxWidgets!')
		
		this.Bind(wx.EVT_MENU, this.OnHello, ID_Hello)
		this.Bind(wx.EVT_MENU, this.OnExit, wx.ID_EXIT)
		this.Bind(wx.EVT_MENU, this.OnAbout, wx.ID_ABOUT)
	}
	OnHello(event as wx.CommandEvent) = {
		Println('Hello world from wxWidgets!')
	}
	OnExit(event as wx.CommandEvent) = {
		this.Close(true)
	}
	OnAbout(event as wx.CommandEvent) = {
		wx.MessageBox("This is a wxWidgets' Hello world sample",
					'About Hello World', wx.OK | wx.ICON_INFORMATION)
	}
}

ID_Hello = 1

wx.ImplementApp(MyApp())
