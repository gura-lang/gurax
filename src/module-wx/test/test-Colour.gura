#!/usr/bin/env gurax
import(wx)

MyApp = class(wx.App) {
	OnInit() = {
		MyFrame(path.BaseName(path.FileName(sys.argv[0])), wx.Point(50, 50), wx.Size(800, 600)).Show()
		true
	}
}

MyFrame = class(wx.Frame) {
	__init__(title as String, pos as wx.Point, size as wx.Size) = {|nil, wx.ID_ANY, title, pos, size|
		PanelTop(this)
	}
}
colours = @{
	wx.NullColour, wx.TransparentColour,
	wx.BLACK, wx.BLUE, wx.GREEN, wx.CYAN, wx.RED, wx.YELLOW, wx.WHITE, wx.LIGHT_GREY
	wx.Colour('#000000')
	wx.Colour('#0000ff')
	wx.Colour('#00ff00')
	wx.Colour('#00ffff')
	wx.Colour('#ff0000')
	wx.Colour('#ff00ff')
	wx.Colour('#ffff00')
	wx.Colour('#ffffff')
	wx.Colour('#c0c0c0')
}

PanelTop = class(wx.Panel) {
	panel as wx.Panel
	textCtrl as wx.TextCtrl
	sliderCtrl_Red as wx.Slider
	sliderCtrl_Green as wx.Slider
	sliderCtrl_Blue as wx.Slider
	label_Name as wx.StaticText
	label_Red as wx.StaticText
	label_Green as wx.StaticText
	label_Blue as wx.StaticText
	__init__(parent as wx.Window) = {|parent|
		sizerTop = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(sizerTop)
		wx.BoxSizer(wx.HORIZONTAL) {|hbox|
			sizerTop.Add(hbox, wx.SizerFlags().Expand())
			wx.StaticText(this, wx.ID_ANY, '', style = wx.BORDER_SUNKEN | wx.ST_NO_AUTORESIZE, size = wx.Size(200, -1)) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Expand())
				this.label_Name = ctrl
			}
			wx.Panel(this, wx.ID_ANY, style = wx.BORDER_SUNKEN, size = wx.Size(-1, 30)) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags(1).Expand().Border(wx.LEFT, 2))
				this.panel = ctrl
			}
		}
		wx.FlexGridSizer(2, wx.Size(12, 2)) {|sizer|
			sizerTop.Add(sizer, wx.SizerFlags().Expand())
			sizer.AddGrowableCol(1);
			wx.StaticText(this, wx.ID_ANY, 'Red') {|ctrl| sizer.Add(ctrl)}
			wx.BoxSizer(wx.HORIZONTAL) {|hbox|
				sizer.Add(hbox, wx.SizerFlags().Expand())
				wx.Slider(this, wx.ID_ANY, 128, 0, 255) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags(1).Expand())
					this.sliderCtrl_Red = ctrl
					wx.EVT_SCROLL(ctrl, this.OnScroll)
				}
				wx.StaticText(this, wx.ID_ANY, '', size = wx.Size(60, -1)) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Expand())
					this.label_Red = ctrl
				}
			}
			wx.StaticText(this, wx.ID_ANY, 'Green') {|ctrl| sizer.Add(ctrl)}
			wx.BoxSizer(wx.HORIZONTAL) {|hbox|
				sizer.Add(hbox, wx.SizerFlags().Expand())
				wx.Slider(this, wx.ID_ANY, 128, 0, 255) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags(1).Expand())
					this.sliderCtrl_Green = ctrl
					wx.EVT_SCROLL(ctrl, this.OnScroll)
				}
				wx.StaticText(this, wx.ID_ANY, '', size = wx.Size(60, -1)) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Expand())
					this.label_Green = ctrl
				}
			}
			wx.StaticText(this, wx.ID_ANY, 'Blue') {|ctrl| sizer.Add(ctrl)}
			wx.BoxSizer(wx.HORIZONTAL) {|hbox|
				sizer.Add(hbox, wx.SizerFlags().Expand())
				wx.Slider(this, wx.ID_ANY, 128, 0, 255) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags(1).Expand())
					this.sliderCtrl_Blue = ctrl
					wx.EVT_SCROLL(ctrl, this.OnScroll)
				}
				wx.StaticText(this, wx.ID_ANY, '', size = wx.Size(60, -1)) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Expand())
					this.label_Blue = ctrl
				}
			}
		}
		wx.ListCtrl(this, wx.ID_ANY, style = wx.LC_REPORT) {|ctrl|
			sizerTop.Add(ctrl, wx.SizerFlags(1).Expand())
			ctrl.SetFont(wx.Font(12, wx.FONTFAMILY_TELETYPE,  wx.FONTSTYLE_NORMAL, wx.FONTWEIGHT_NORMAL))
			ctrl.AppendColumn('NAME', wx.LIST_FORMAT_LEFT, 300)
			ctrl.AppendColumn('CSS', wx.LIST_FORMAT_LEFT, 250)
			ctrl.AppendColumn('HTML', wx.LIST_FORMAT_LEFT, 200)
			colours.Each {|colour, idx|
				ctrl.InsertItem(idx, colour.GetAsString())
				ctrl.SetItem(idx, 1, colour.GetAsString(wx.C2S_CSS_SYNTAX))
				ctrl.SetItem(idx, 2, colour.GetAsString(wx.C2S_HTML_SYNTAX))
			}
			ctrl.Bind(wx.EVT_LIST_ITEM_SELECTED, this.OnListItemSelected)
		}
		this.UpdateColour()
	}
	UpdateColour() = {
		colour = wx.Colour(
				this.sliderCtrl_Red.GetValue(),
				this.sliderCtrl_Green.GetValue(),
				this.sliderCtrl_Blue.GetValue())
		this.label_Name.SetLabel(colour.GetAsString())
		this.label_Red.SetLabel(Format('%d', colour.Red()))
		this.label_Green.SetLabel(Format('%d', colour.Green()))
		this.label_Blue.SetLabel(Format('%d', colour.Blue()))
		this.panel.SetBackgroundColour(colour)
		this.panel.Refresh()
	}
	OnScroll(event as wx.Event) = this.UpdateColour()
	OnListItemSelected(event as wx.ListEvent) = {
		colour = colours[event.GetIndex()]
		this.sliderCtrl_Red.SetValue(colour.Red())
		this.sliderCtrl_Green.SetValue(colour.Green())
		this.sliderCtrl_Blue.SetValue(colour.Blue())
		this.UpdateColour()
	}
}

wx.ImplementApp(MyApp())
