#!/usr/bin/env gurax
import(wx)

monthNames = [
	'January', 'February', 'March', 'April', 'May', 'June'
	'July', 'August', 'September', 'October', 'November', 'December'
]

MyApp = class(wx.App) {
	OnInit() = {
		MyFrame(path.BaseName(path.FileName(sys.argv[0])), wx.Point(50, 50), wx.Size(800, 600)).Show()
		true
	}
}

MyFrame = class(wx.Frame) {
	__init__(title as String, pos as wx.Point, size as wx.Size) = {|nil, wx.ID_ANY, title, pos, size|
		PanelTop(this)
	}
}

PanelTop = class(wx.Panel) {
	textCtrl as wx.TextCtrl
	__init__(parent as wx.Window) = {|parent|
		sizerTop = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(sizerTop)
		tupples = [
			('(no flags)', 0)
			('CB_DROPDOWNB', wx.CB_DROPDOWN)
			('CB_READONLY', wx.CB_READONLY)
		]
		wx.FlexGridSizer(2, wx.Size(16, 8)) {|sizer|
			sizerTop.Add(sizer, wx.SizerFlags().Expand().Border(wx.TOP | wx.BOTTOM, 8))
			sizer.AddGrowableCol(1)
			tupples.Each {|tuple|
				[label, style] = tuple
				wx.StaticText(this, wx.ID_ANY, label) {|ctrl|
					sizer.Add(ctrl, wx.SizerFlags().Expand())
				}
				wx.ComboBox(this, wx.ID_ANY, choices = monthNames, style = style) {|ctrl|
					sizer.Add(ctrl, wx.SizerFlags().Expand())
					wx.EVT_COMBOBOX(ctrl, this.OnComboBox, userData = label)
					wx.EVT_TEXT(ctrl, this.OnText, userData = label)
					wx.EVT_TEXT_ENTER(ctrl, this.OnTextEnter, userData = label)
					wx.EVT_COMBOBOX_DROPDOWN(ctrl, this.OnComboBoxDropDown, userData = label)
					wx.EVT_COMBOBOX_CLOSEUP(ctrl, this.OnComboBoxCloseUp, userData = label)
				}
			}
		}
		wx.TextCtrl(this, wx.ID_ANY, style = wx.TE_MULTILINE) {|ctrl|
			sizerTop.Add(ctrl, wx.SizerFlags(1).Expand())
			this.textCtrl = ctrl
		}
	}
	Printf(format as String, args*) = this.textCtrl.WriteText(Format(format, args*))
	OnComboBox(event as wx.CommandEvent) = {
		label = event.GetEventUserData()
		this.Printf('EVT_COMBOBOX(%s)\n', label)		
	}
	OnText(event as wx.CommandEvent) = {
		label = event.GetEventUserData()
		this.Printf('EVT_TEXT(%s)\n', label)		
	}
	OnTextEnter(event as wx.CommandEvent) = {
		label = event.GetEventUserData()
		this.Printf('EVT_TEXT_ENTER(%s)\n', label)		
	}
	OnComboBoxDropDown(event as wx.CommandEvent) = {
		label = event.GetEventUserData()
		this.Printf('EVT_COMBOBOX_DROPDOWN(%s)\n', label)		
	}
	OnComboBoxCloseUp(event as wx.CommandEvent) = {
		label = event.GetEventUserData()
		this.Printf('EVT_COMBOBOX_CLOSEUP(%s)\n', label)		
	}
}

wx.ImplementApp(MyApp())
