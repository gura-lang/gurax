#!/usr/bin/env gurax
import(wx)
import(image)

MyApp = class(wx.App) {
	OnInit() = {
		MyFrame(path.BaseName(path.FileName(sys.argv[0])), wx.Point(50, 50), wx.Size(800, 600)).Show()
		true
	}
}

MyFrame = class(wx.Frame) {
	__init__(title as String, pos as wx.Point, size as wx.Size) = {|nil, wx.ID_ANY, title, pos, size|
		PanelTop(this)
	}
}

PanelTop = class(wx.Panel) {
	textCtrl as wx.TextCtrl
	__init__(parent as wx.Window) = {|parent|
		sizerTop = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(sizerTop)
		wx.BoxSizer(wx.HORIZONTAL) {|hbox|
			sizerTop.Add(hbox, wx.SizerFlags(5).Expand())
			wx.BoxSizer(wx.VERTICAL) {|vbox|
				hbox.Add(vbox, wx.SizerFlags(1).Expand())
				vbox.Add(wx.StaticText(this, wx.ID_ANY, 'Notebook'), wx.SizerFlags().Expand())
				wx.Notebook(this, wx.ID_ANY) {|book|
					vbox.Add(book, wx.SizerFlags(1).Expand())
					this.SetupPages(book)
					book.Bind(wx.EVT_NOTEBOOK_PAGE_CHANGED, this.OnBook)
					book.Bind(wx.EVT_NOTEBOOK_PAGE_CHANGING, this.OnBook)
				}
				vbox.Add(wx.StaticText(this, wx.ID_ANY, 'Treebook'), wx.SizerFlags().Expand())
				wx.Treebook(this, wx.ID_ANY) {|book|
					vbox.Add(book, wx.SizerFlags(1).Expand())
					['Page1', 'Page2', 'Page3', 'Page4', 'Page5'].Each {|label|
						wx.Panel(book) {|panel|
							book.AddPage(panel, label)
							wx.BoxSizer(wx.VERTICAL) {|vbox|
								panel.SetSizer(vbox)
								vbox.Add(wx.Button(panel, wx.ID_ANY, 'Content of ' + label), wx.SizerFlags(1).Expand())
							}
							book.AddSubPage(panel, 'hoge')
						}
					}
					book.Bind(wx.EVT_TREEBOOK_PAGE_CHANGED, this.OnBook)
					book.Bind(wx.EVT_TREEBOOK_PAGE_CHANGING, this.OnBook)
					book.Bind(wx.EVT_TREEBOOK_NODE_COLLAPSED, this.OnBook)
					book.Bind(wx.EVT_TREEBOOK_NODE_EXPANDED, this.OnBook)
				}
				vbox.Add(wx.StaticText(this, wx.ID_ANY, 'Listbook'), wx.SizerFlags().Expand())
				wx.Listbook(this, wx.ID_ANY) {|book|
					vbox.Add(book, wx.SizerFlags(1).Expand())
					this.SetupPages(book)
				}
			}
			wx.BoxSizer(wx.VERTICAL) {|vbox|
				hbox.Add(vbox, wx.SizerFlags(1).Expand().Border(wx.LEFT, 16))
				vbox.Add(wx.StaticText(this, wx.ID_ANY, 'Simplebook'), wx.SizerFlags().Expand())
				wx.Simplebook(this, wx.ID_ANY) {|book|
					vbox.Add(book, wx.SizerFlags(1).Expand())
					this.SetupPages(book)
				}
				vbox.Add(wx.StaticText(this, wx.ID_ANY, 'Toolbook'), wx.SizerFlags().Expand())
				wx.Toolbook(this, wx.ID_ANY) {|book|
					vbox.Add(book, wx.SizerFlags(1).Expand())
					imageList = wx.ImageList(16, 16)
					book.SetImageList(imageList);
					imageList.Add(image.folder_image)
					imageList.Add(image.report_picture)
					imageList.Add(image.photo)
					imageList.Add(image.resultset_next)
					imageList.Add(image.resultset_previous)
					//this.SetupPages(book)
				}
				vbox.Add(wx.StaticText(this, wx.ID_ANY, 'Choicebook'), wx.SizerFlags().Expand())
				wx.Choicebook(this, wx.ID_ANY) {|book|
					vbox.Add(book, wx.SizerFlags(1).Expand())
					this.SetupPages(book)
				}
			}
		}
		wx.TextCtrl(this, wx.ID_ANY, style = wx.TE_MULTILINE) {|ctrl|
			sizerTop.Add(ctrl, wx.SizerFlags(1).Expand().Border(wx.TOP, 4))
			this.textCtrl = ctrl
		}
	}
	OnBook(event as wx.BookCtrlEvent) = {
		eventName = event.GetEventType().name
		this.Printf('%s\n', eventName)
	}
	Printf(format as String, args*) = this.textCtrl.WriteText(Format(format, args*))
	SetupPages(book as wx.BookCtrlBase) = {
		['Page1', 'Page2', 'Page3', 'Page4', 'Page5'].Each {|label|
			wx.Panel(book) {|panel|
				book.AddPage(panel, label)
				wx.BoxSizer(wx.VERTICAL) {|vbox|
					panel.SetSizer(vbox)
					vbox.Add(wx.Button(panel, wx.ID_ANY, 'Content of ' + label), wx.SizerFlags(1).Expand())
				}
			}
		}
	}
}

wx.ImplementApp(MyApp())
