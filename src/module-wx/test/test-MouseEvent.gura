#!/usr/bin/env gurax
import(wx)

MyApp = class(wx.App) {
	OnInit() = {
		MyFrame(path.BaseName(path.FileName(sys.argv[0])), wx.Point(50, 50), wx.Size(800, 600)).Show()
		true
	}
}

MyFrame = class(wx.Frame) {
	__init__(title as String, pos as wx.Point, size as wx.Size) = {|nil, wx.ID_ANY, title, pos, size|
		PanelTop(this)
	}
}

PanelTop = class(wx.Panel) {
	textCtrl as wx.TextCtrl
	__init__(parent as wx.Window) = {|parent|
		sizerTop = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(sizerTop)
		wx.StaticText(this, wx.ID_ANY, 'Move Mouse in This Window') {|ctrl|
			sizerTop.Add(ctrl, wx.SizerFlags().Expand())
		}
		wx.Panel(this, wx.ID_ANY) {|ctrl|
			sizerTop.Add(ctrl, wx.SizerFlags(1).Expand())
			ctrl.Bind(wx.EVT_LEFT_DOWN,		this.OnMouse)
			ctrl.Bind(wx.EVT_LEFT_UP,		this.OnMouse)
			ctrl.Bind(wx.EVT_LEFT_DCLICK,	this.OnMouse)
			ctrl.Bind(wx.EVT_MIDDLE_DOWN,	this.OnMouse)
			ctrl.Bind(wx.EVT_MIDDLE_UP,		this.OnMouse)
			ctrl.Bind(wx.EVT_MIDDLE_DCLICK,	this.OnMouse)
			ctrl.Bind(wx.EVT_RIGHT_DOWN,	this.OnMouse)
			ctrl.Bind(wx.EVT_RIGHT_UP,		this.OnMouse)
			ctrl.Bind(wx.EVT_RIGHT_DCLICK,	this.OnMouse)
			ctrl.Bind(wx.EVT_AUX1_DOWN,		this.OnMouse)
			ctrl.Bind(wx.EVT_AUX1_UP,		this.OnMouse)
			ctrl.Bind(wx.EVT_AUX1_DCLICK,	this.OnMouse)
			ctrl.Bind(wx.EVT_AUX2_DOWN,		this.OnMouse)
			ctrl.Bind(wx.EVT_AUX2_UP,		this.OnMouse)
			ctrl.Bind(wx.EVT_AUX2_DCLICK,	this.OnMouse)
			ctrl.Bind(wx.EVT_MOTION,		this.OnMouse)
			ctrl.Bind(wx.EVT_ENTER_WINDOW,	this.OnMouse)
			ctrl.Bind(wx.EVT_LEAVE_WINDOW,	this.OnMouse)
			ctrl.Bind(wx.EVT_MOUSEWHEEL,	this.OnMouse)
		}
		wx.TextCtrl(this, wx.ID_ANY, style = wx.TE_MULTILINE) {|ctrl|
			sizerTop.Add(ctrl, wx.SizerFlags(1).Expand())
			this.textCtrl = ctrl
		}
	}
	OnMouse(event as wx.MouseEvent) = {
		eventName = event.GetEventType().name
		pt = event.GetPosition()
		this.Printf('%s(x = %d, y = %d, wheelDelta = %d, wheelRotation = %d, wheelAxis = %d)%s%s%s%s%s%s\n', eventName,
			event.GetX(), event.GetY(), event.GetWheelDelta(), event.GetWheelRotation(), event.GetWheelAxis(),
			cond(event.ControlDown(), ', Control', '')
			cond(event.RawControlDown(), ', RawControl', '')
			cond(event.ShiftDown(), ', Shift', '')
			cond(event.MetaDown(), ', Meta', '')
			cond(event.AltDown(), ', Alt', '')
			cond(event.CmdDown(), ', Cmd', '')
			)
	}
	Printf(format as String, args*) = this.textCtrl.WriteText(Format(format, args*))
}

wx.ImplementApp(MyApp())
