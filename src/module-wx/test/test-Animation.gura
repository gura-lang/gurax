#!/usr/bin/env gurax
import(wx)

MyApp = class(wx.App) {
	OnInit() = {
		MyFrame(path.BaseName(path.FileName(sys.argv[0])), wx.Point(50, 50), wx.Size(800, 600)).Show()
		true
	}
}

MyFrame = class(wx.Frame) {
	__init__(title as String, pos as wx.Point, size as wx.Size) = {|nil, wx.ID_ANY, title, pos, size|
		PanelTop(this)
	}
}

PanelTop = class(wx.Panel) {
	__init__(parent as wx.Window) = {|parent|
		sizerTop = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(sizerTop)
		dirName = '../../../test/test-gif'
		wx.GridSizer(6, 8, 8) {|sizer|
			sizerTop.Add(sizer, wx.SizerFlags(1).Expand())
			['buta-ani.gif', 'chicken-anim.gif', 'nekorun-ani02.gif', 'nezumi-ani05.gif', 'roba-ani.gif', 'tanuki-ani01.gif'].Cycle(6 * 3).Each {|fileName, i|
				wx.BoxSizer(wx.VERTICAL) {|vbox|
					sizer.Add(vbox, wx.SizerFlags(1).Expand())
					vbox.AddStretchSpacer(1)
					anim = wx.Animation(path.Join(dirName, fileName))
					wx.AnimationCtrl(this, wx.ID_ANY, anim) {|ctrl|
						vbox.Add(ctrl, wx.SizerFlags().Align(wx.CENTRE))
						animationCtrl = ctrl
					}
					vbox.AddStretchSpacer(1)
					wx.StaticText(this, wx.ID_ANY, Format('Delay = %d', anim.GetDelay(0))) {|ctrl|
						vbox.Add(ctrl, wx.SizerFlags().Expand())
					}
					wx.StaticText(this, wx.ID_ANY, Format('FrameCount = %d', anim.GetFrameCount())) {|ctrl|
						vbox.Add(ctrl, wx.SizerFlags().Expand())
					}
					size = anim.GetSize()
					wx.StaticText(this, wx.ID_ANY, Format('Size = [%d, %d]', size.GetWidth(), size.GetHeight())) {|ctrl|
						vbox.Add(ctrl, wx.SizerFlags().Expand())
					}
					wx.Button(this, wx.ID_ANY, 'Play') {|ctrl|
						vbox.Add(ctrl, wx.SizerFlags().Expand())
						wx.EVT_BUTTON(ctrl, this.OnButton_Play, userData = animationCtrl)
					}
					wx.Button(this, wx.ID_ANY, 'Stop') {|ctrl|
						vbox.Add(ctrl, wx.SizerFlags().Expand())
						wx.EVT_BUTTON(ctrl, this.OnButton_Stop, userData = animationCtrl)
					}
				}
			}
		}
	}
	OnButton_Play(event as wx.CommandEvent) = {
		animationCtrl = event.GetEventUserData()
		animationCtrl.Play()
	}
	OnButton_Stop(event as wx.CommandEvent) = {
		animationCtrl = event.GetEventUserData()
		animationCtrl.Stop()
	}
}

wx.ImplementApp(MyApp())
