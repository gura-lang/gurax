#!/usr/bin/env gurax
import(wx)

MyApp = class(wx.App) {
	OnInit() = {
		MyFrame(path.BaseName(path.FileName(sys.argv[0])), wx.Point(50, 50), wx.Size(800, 600)).Show()
		true
	}
}

MyFrame = class(wx.Frame) {
	__init__(title as String, pos as wx.Point, size as wx.Size) = {|nil, wx.ID_ANY, title, pos, size|
		PanelTop(this)
	}
}

PanelTop = class(wx.Panel) {
	panel as wx.Panel
	sash1 as wx.SashLayoutWindow
	sash2 as wx.SashLayoutWindow
	__init__(parent as wx.Window) = {|parent|
		wx.SashLayoutWindow(this, wx.ID_ANY, style = wx.CLIP_CHILDREN) {|sash|
			sash.SetDefaultSize(wx.Size(1000, 100));
			sash.SetOrientation(wx.LAYOUT_HORIZONTAL);
			sash.SetAlignment(wx.LAYOUT_TOP);
			sash.SetSashVisible(wx.SASH_BOTTOM, true);
			this.sash1 = sash
		}
		wx.SashLayoutWindow(this, wx.ID_ANY, style = wx.CLIP_CHILDREN) {|sash|
			sash.SetDefaultSize(wx.Size(1000, 100));
			sash.SetOrientation(wx.LAYOUT_HORIZONTAL);
			sash.SetAlignment(wx.LAYOUT_TOP);
			sash.SetSashVisible(wx.SASH_BOTTOM, true);
			this.sash2 = sash
		}
		wx.StaticText(this.sash1, wx.ID_ANY, 'sash1', style = wx.BORDER_SUNKEN)
		wx.StaticText(this.sash2, wx.ID_ANY, 'sash2', style = wx.BORDER_SUNKEN)
		wx.Panel(this, wx.ID_ANY, style = wx.BORDER_SUNKEN) {|panel|
			this.panel = panel
		}
		wx.EVT_SIZE(this, this.OnSize)
		wx.EVT_SASH_DRAGGED(this, this.OnSashDragged)
	}
	OnSize(event) = {
		wx.LayoutAlgorithm().LayoutWindow(this, this.panel)
		event.Skip()
	}
	OnSashDragged(event) = {
		if (event.GetDragStatus() != wx.SASH_STATUS_OUT_OF_RANGE) {
			id = event.GetId()
			if (id == this.sash1.GetId()) {
				htSashTop = event.GetDragRect().GetHeight() + 2
				this.sash1.SetDefaultSize(wx.Size(1000, htSashTop))
			} elsif (id == this.sash2.GetId()) {
				htSashTop = event.GetDragRect().GetHeight() + 2
				this.sash2.SetDefaultSize(wx.Size(1000, htSashTop))
			}
			wx.LayoutAlgorithm().LayoutWindow(this, this.panel)
			this.panel.Refresh()
		}
	}
}

wx.ImplementApp(MyApp())
