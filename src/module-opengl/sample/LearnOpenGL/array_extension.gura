#!/usr/bin/env gurax

Array.Rotation(elemType:Symbol, angle:Number, ax:Number, ay:Number, az:Number):static = {
	axis = [ax, ay, az]
	[nx, ny, nz] = axis / math.Sqrt((axis * axis).Sum())
	cos = math.Cos(angle), sin = math.Sin(angle)
	Array(elemType,
		[[cos + nx * nx * (1 - cos), nx * ny * (1 - cos) - nz * sin, nz * nx * (1 - cos) + ny * sin, 0]
		 [nx * ny * (1 - cos) + nz * sin, cos + ny * ny * (1 - cos), ny * nz * (1 - cos) - nx * sin, 0]
		 [nz * nx * (1 - cos) - ny * sin, ny * nz * (1 - cos) + nx * sin, cos + nz * nz * (1 - cos), 0]
		 [0, 0, 0, 1]])
}

Array.Translation(elemType:Symbol, tx:Number, ty:Number, tz:Number):static = {
	Array(elemType,
		[[1, 0, 0, tx]
		 [0, 1, 0, ty]
		 [0, 0, 1, tz]
		 [0, 0, 0, 1]])
}

Array.Scale(elemType:Symbol, sx:Number, sy:Number, sz:Number):static = {
	Array(elemType,
		[[sx, 0, 0, 0]
		 [0, sy, 0, 0]
		 [0, 0, sz, 0]
		 [0, 0, 0, 1]])
}

Array.Ortho(elemType:Symbol, left:Number, right:Number, bottom:Number, top:Number, near:Number, far:Number):static = {
	Array(elemType,
		[[2 / (right - left), 0, 0, -(right + left) / (right - left)]
		 [0, 2 / (top - bottom), 0, -(top + bottom) / (top - bottom)]
		 [0, 0, -2 / (far - near), -(far + near) / (far - near)]
		 [0, 0, 0, 1]])
}

Array.Perspective(elemType:Symbol, fieldOfView:Number, aspect:Number, near:Number, far:Number):static = {
	top = near * math.Tan(fieldOfView / 2)
	right = top * aspect
	Array(elemType,
		[[near / right, 0, 0, 0]
		 [0, near / top, 0, 0]
		 [0, 0, -(far + near) / (far - near), -2 * far * near / (far - near)]
		 [0, 0, -1, 0]])
}

Array.LookAt(elemType:Symbol, px:Number, py:Number, pz:Number,
			tx:Number, ty:Number, tz:Number, ux:Number, uy:Number, uz:Number):static = {
	dVec = [px, py, pz] - [tx, ty, tz]
	[dx, dy, dz] = dVec / math.Sqrt((dVec * dVec).Sum())
	rVec = [uy * dz - uz * dy, uz * dx - ux * dz, ux * dy - uy * dx]
	[rx, ry, rz] = rVec / math.Sqrt((rVec * rVec).Sum())
	[ux, uy, uz] = [dy * rz - dz * ry, dz * rx - dx * rz, dx * ry - dy * rx]
	rtn = Array(elemType, [[rx, ry, rz, 0], [ux, uy, uz, 0], [dx, dy, dz, 0], [0, 0, 0, 1]])
	rtn |.| Array(elemType, [[1, 0, 0, -px], [0, 1, 0, -py], [0, 0, 1, -pz], [0, 0, 0, 1]])
}
