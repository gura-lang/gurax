#!/usr/bin/env gurax
import(opengl) {*}

Shader = class {
	ID:Number
	// constructor generates the shader on the fly
	// ------------------------------------------------------------------------
	__init__(vertexStream:Stream, fragmentStream:Stream) = {
		// convert stream into string
		vertexCode = vertexStream.ReadText();
		fragmentCode = fragmentStream.ReadText();
		// vertex shader
		shaderVertex = glCreateShader(GL_VERTEX_SHADER);
		this.CompileShader(shaderVertex, vertexCode, 'VERTEX')
		// fragment Shader
		shaderFragment = glCreateShader(GL_FRAGMENT_SHADER);
		this.CompileShader(shaderFragment, fragmentCode, 'FRAGMENT')
		// shader Program
		this.ID = glCreateProgram();
		glAttachShader(this.ID, shaderVertex);
		glAttachShader(this.ID, shaderFragment);
		glLinkProgram(this.ID);
		success = @int {|v| glGetProgramiv(this.ID, GL_LINK_STATUS, v), v[0]}
		if (success == 0) {
			infoLog = @char(1024)
			glGetProgramInfoLog(this.ID, infoLog.bytes, nil, infoLog);
			Println("ERROR::PRPGRAM_LINKING_ERROR of type: ", 'PROGRAM')
			Println(infoLog)
			Println("-- --------------------------------------------------- -- ")
		}
		// delete the shaders as they're linked into our ID now and no longer necessary
		glDeleteShader(shaderVertex);
		glDeleteShader(shaderFragment);
	}
	// activate the shader
	// ------------------------------------------------------------------------
	use() = {
		glUseProgram(this.ID)
	}
	// utility uniform functions
	// ------------------------------------------------------------------------
	setBool(name:String, value:Bool) = {         
		glUniform1i(glGetUniformLocation(this.ID, name), value) 
	}
	// ------------------------------------------------------------------------
	setInt(name:String, value:Number) = {
		glUniform1i(glGetUniformLocation(this.ID, name), value) 
	}
	// ------------------------------------------------------------------------
	setFloat(name:String, value:Number) = {
		glUniform1f(glGetUniformLocation(this.ID, name), value) 
	}
	// ------------------------------------------------------------------------
	setVec2(name:String, x:Number, y:Number) = {
		uniformLoc = glGetUniformLocation(this.ID, name)
		glUniform2f(uniformLoc, x, y)
	}
	// ------------------------------------------------------------------------
	setVec3(name:String, x:Number, y:Number, z:Number) = {
		uniformLoc = glGetUniformLocation(this.ID, name)
		glUniform3f(uniformLoc, x, y, z)
	}
	// ------------------------------------------------------------------------
	setVec4(name:String, x:Number, y:Number, z:Number, w:Number) = {
		uniformLoc = glGetUniformLocation(this.ID, name)
		glUniform4f(uniformLoc, x, y, z, w)
	}
	// ------------------------------------------------------------------------
	setArray(name:String, array:Array) = {
		uniformLoc = glGetUniformLocation(this.ID, name)
		if (array.elemType == `float) {
			if (array.VerifyShape(2)) {
				glUniform2fv(uniformLoc, 1, array.p)
			} elsif (array.VerifyShape(3)) {
				glUniform3fv(uniformLoc, 1, array.p)
			} elsif (array.VerifyShape(4)) {
				glUniform4fv(uniformLoc, 1, array.p)
			} elsif (array.VerifyShape(2, 2)) {
				glUniformMatrix2fv(uniformLoc, 1, GL_TRUE, array.p)
			} elsif (array.VerifyShape(3, 3)) {
				glUniformMatrix3fv(uniformLoc, 1, GL_TRUE, array.p)
			} elsif (array.VerifyShape(4, 4)) {
				glUniformMatrix4fv(uniformLoc, 1, GL_TRUE, array.p)
			}
		}
	}
	CompileShader(shader:Number, shaderCode:String, typeName:String) = {
		glShaderSource(shader, [shaderCode], []);
		glCompileShader(shader);
		success = @int {|v| glGetShaderiv(shader, GL_COMPILE_STATUS, v), v[0]}
		if (success == 0) {
			infoLog = @char(1024)
			glGetShaderInfoLog(shader, infoLog.bytes, nil, infoLog);
			Println("ERROR::SHADER_COMPILATION_ERROR of type: ", typeName)
			Println(infoLog)
			Println("-- --------------------------------------------------- -- ")
		}
	}
}
