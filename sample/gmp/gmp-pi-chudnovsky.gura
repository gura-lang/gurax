#!/usr/bin/env gurax
// http://d.hatena.ne.jp/itchyny/20120304/1330870932
import(gmp)

[A, B, C, C3over24] = 0L

PQT = struct { P as gmp.Int, Q as gmp.Int, T as gmp.Int }

ComputePQT(n1 as Number, n2 as Number) = {
	if (n1 + 1 == n2) {
		P = -(2 * n2 - 1)
		P *= (6 * n2 - 5)
		P *= (6 * n2 - 1)
		Q = C3over24 * n2 * n2 * n2
		T = (A + B * n2) * P
	} else {
		m = Int((n1 + n2) / 2);
		PQT1 = ComputePQT(n1, m);
		PQT2 = ComputePQT(m, n2);
		P = PQT1.P * PQT2.P;
		Q = PQT1.Q * PQT2.Q;
		T = PQT1.T * PQT2.Q + PQT1.P * PQT2.T;
	}
	PQT(P, Q, T)
}

main(streamOut:w as Stream, digits as Number) = {
	blockCols = 5
	blockRows = 20
	gmp.Float.default_prec = Int(digits * math.Log(10) / math.Log(2))
	n = Int(digits / 14)
	A:extern = 13591409L
	B:extern = 545140134L
	C:extern = 640320L
	C3over24:extern = C * C * C / 24;
	PQT = ComputePQT(0, n);
	pi = math.Sqrt(gmp.Float(C));
	pi *= C * PQT.Q;
	pi /= (PQT.T + A * PQT.Q) * 12;
	streamOut.Println('3')
	streamOut.Print(Format('%.*f', digits, pi).Mid(2).Fold(10).\
			Fold(blockCols):*Join(' ').Fold(blockRows):*Join('\n'), '\n\n')
}

fileNameOut = path.BaseName(path.FileName(sys.argv[0])) + '.txt'
digits = cond(sys.argv.len < 2, 10000, Int(sys.argv[1]))
main(fileNameOut, digits)
Printf('%d digits of pi was written in %s.\n', digits, fileNameOut)
