#!/usr/bin/env gurax
// pi calculation by Gibbons algorithm
import(gmp)

GeneratePI() = {
	[q, r, t, i] = [1L, 180L, 60L, 2L]
	repeat ():iter {
		u = 3 * (3 * i + 1) * (3 * i + 2)
		y = (q * (27 * i - 12) + 5 * r) / (5 * t)
		[q, r] = [
			10 * q * i * (2 * i - 1)
			10 * u * (q * (5 * i - 2) + r - y * t)
		]
		t *= u
		i += 1
		y
	}
}

main(fileNameOut as String, digits as Number) = {
	blockCols = 5
	blockRows = 20
	Open(fileNameOut, 'w') {|f|
		f.Println('3')
		f.Print(Format('%d', GeneratePI().Offset(1).Head(digits)).\
			Fold(10):*Join().Fold(blockCols):*Join(' ').Fold(blockRows):*Join('\n'), '\n\n')
	}
}

fileNameOut = path.BaseName(path.FileName(sys.argv[0])) + '.txt'
digits = cond(sys.argv.len < 2, 10000, Int(sys.argv[1]))
main(fileNameOut, digits)
Printf('%d digits of pi was written in %s.\n', digits, fileNameOut)
