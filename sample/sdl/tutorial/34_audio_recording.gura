#!/usr/bin/env gurax
/*This source code copyrighted by Lazy Foo' Productions (2004-2020)
and may not be redistributed without written permission.*/
// Translated to Gura by ypsitau.
// Don't make this public outside Git repository since it hasn't received permission by the author.

import(sdl) {*}
import(png)

SCREEN_WIDTH = 640;
SCREEN_HEIGHT = 480;

//Maximum number of supported recording devices
MAX_RECORDING_DEVICES = 10;

//Maximum recording time
MAX_RECORDING_SECONDS = 5;

//Maximum recording time plus padding
RECORDING_BUFFER_SECONDS = MAX_RECORDING_SECONDS + 1;

//The various recording actions we can take
/*
enum RecordingState
{
	SELECTING_DEVICE,
	STOPPED,
	RECORDING,
	RECORDED,
	PLAYBACK,
	ERROR
};
*/

//Texture wrapper class
LTexture = class {
	//Initializes variables
	__init__() = {
		//Initialize
		this.mTexture = nil;
		this.mWidth = 0;
		this.mHeight = 0;
	}

	//Deallocates memory
	__del__() = {
		this.free()
	};

	//Loads image at specified path
	loadFromFile(path as String) = {
		//Get rid of preexisting texture
		this.free();

		//The final texture
		newTexture = nil;

		//Load image at specified path
		loadedSurface = IMG_Load(path);
		if (!loadedSurface) {
			Printf("Unable to load image %s! SDL_image Error: %s\n", path, IMG_GetError());
		} else {
			//Color key image
			SDL_SetColorKey(loadedSurface, SDL_TRUE, SDL_MapRGB(loadedSurface.format, 0, 0xFF, 0xFF));

			//Create texture from surface pixels
			newTexture = SDL_CreateTextureFromSurface(gRenderer, loadedSurface);
			if (!newTexture) {
				Printf("Unable to create texture from %s! SDL Error: %s\n", path, SDL_GetError());
			} else {
				//Get image dimensions
				this.mWidth = loadedSurface.w;
				this.mHeight = loadedSurface.h;
			}

			//Get rid of old loaded surface
			SDL_FreeSurface(loadedSurface);
		}

		//Return success
		this.mTexture = newTexture;
		this.mTexture != nil;
	}

	//Creates image from font string
	loadFromRenderedText(textureText as String, textColor as SDL_Color) = {
		//Get rid of preexisting texture
		this.free();

		//Render text surface
		textSurface = TTF_RenderText_Solid(gFont, textureText, textColor);
		if (!textSurface) {
			Printf("Unable to render text surface! SDL_ttf Error: %s\n", TTF_GetError());
		} else {
			//Create texture from surface pixels
			this.mTexture = SDL_CreateTextureFromSurface(gRenderer, textSurface);
			if (!this.mTexture) {
				Printf("Unable to create texture from rendered text! SDL Error: %s\n", SDL_GetError());
			} else {
				//Get image dimensions
				this.mWidth = textSurface.w;
				this.mHeight = textSurface.h;
			}

			//Get rid of old surface
			SDL_FreeSurface(textSurface);
		}
		
		//Return success
		this.mTexture != nil;
	}

	//Deallocates texture
	free() = {
		if (this.mTexture) {
			SDL_DestroyTexture(this.mTexture);
			this.mTexture = nil;
			this.mWidth = 0;
			this.mHeight = 0;
		}
	}

	//Set color modulation
	setColor(red as Number, green as Number, blue as Number) = {
		//Modulate texture
		SDL_SetTextureColorMod(this.mTexture, red, green, blue);
	}

	//Set blending
	setBlendMode(blending as Number) = {
		//Set blending function
		SDL_SetTextureBlendMode(this.mTexture, blending);
	}
			
	//Set alpha modulation
	setAlpha(alpha as Number) = {
		//Modulate texture alpha
		SDL_SetTextureAlphaMod(this.mTexture, alpha);
	}

	//Renders texture at given point
	render(x as Number, y as Number, clip? as SDL_Rect, angle as Number = 0.0, center? as SDL_Point, flip as Number = SDL_FLIP_NONE) = {
		//Set rendering space and render to screen
		renderQuad = SDL_Rect(x, y, this.mWidth, this.mHeight);

		//Set clip rendering dimensions
		if (clip) {
			renderQuad.w = clip.w;
			renderQuad.h = clip.h;
		}

		//Render to screen
		SDL_RenderCopyEx(gRenderer, this.mTexture, clip, renderQuad, angle, center, flip);
	}

	//Gets image dimensions
	getWidth() = this.mWidth;
	getHeight() = this.mHeight;

	//The actual hardware texture
	mTexture as SDL_Texture;

	//Image dimensions
	mWidth as Number;
	mHeight as Number;
};

//The window we'll be rendering to
gWindow = nil;

//The window renderer
gRenderer = nil;

//Globally used font
gFont = nil;
gTextColor = SDL_Color(0, 0, 0, 0xFF);

//Scene sprites
gPromptTexture = LTexture();

//The text textures that specify recording device names
gDeviceTextures = Dim(MAX_RECORDING_DEVICES) { LTexture() }

//Number of available devices
gRecordingDeviceCount = 0;

//Recieved audio spec
gReceivedRecordingSpec = SDL_AudioSpec();
gReceivedPlaybackSpec = SDL_AudioSpec();

//Recording data buffer
gRecordingBuffer = nil;

//Size of data buffer
gBufferByteSize = 0;

//Position in data buffer
//userdata.bufferBytePosition = 0;

//Maximum position in data buffer for recording
gBufferByteMaxPosition = 0;

UserData = struct {
	bufferBytePosition as Number = 0
}

init() = {
	//Initialization flag
	success = true;

	//Initialize SDL
	if (SDL_Init(SDL_INIT_VIDEO | SDL_INIT_AUDIO) < 0) {
		Printf("SDL could not initialize! SDL_Error: %s\n", SDL_GetError());
		success = false;
	} else {
		//Set texture filtering to linear
		if (!SDL_SetHint(SDL_HINT_RENDER_SCALE_QUALITY, "1")) {
			Printf("Warning: Linear texture filtering not enabled!");
		}

		//Create window
		gWindow:extern = gWindow = SDL_CreateWindow("SDL Tutorial", SDL_WINDOWPOS_UNDEFINED, SDL_WINDOWPOS_UNDEFINED, SCREEN_WIDTH, SCREEN_HEIGHT, SDL_WINDOW_SHOWN);
		if (!gWindow)  {
			Printf("Window could not be created! SDL_Error: %s\n", SDL_GetError());
			success = false;
		} else {
			//Create renderer for window
			gRenderer:extern = SDL_CreateRenderer(gWindow, -1, SDL_RENDERER_ACCELERATED | SDL_RENDERER_PRESENTVSYNC);
			if (!gRenderer) {
				Printf("Renderer could not be created! SDL Error: %s\n", SDL_GetError());
				success = false;
			} else {
				//Initialize renderer color
				SDL_SetRenderDrawColor(gRenderer, 0xFF, 0xFF, 0xFF, 0xFF);

				//Initialize PNG loading
				imgFlags = IMG_INIT_PNG;
				if ((IMG_Init(imgFlags) & imgFlags) == 0) {
					Printf("SDL_image could not initialize! SDL_image Error: %s\n", IMG_GetError());
					success = false;
				}
	
				 //Initialize SDL_ttf
				if (TTF_Init() == -1) {
					Printf("SDL_ttf could not initialize! SDL_ttf Error: %s\n", TTF_GetError());
					success = false;
				}
			}
		}
	}

	success
}

loadMedia() = {
	//Loading success flag
	success = true;

	//Open the font
	gFont:extern = TTF_OpenFont("34_audio_recording/lazy.ttf", 28);
	if (!gFont) {
		Printf("Failed to load lazy font! SDL_ttf Error: %s\n", TTF_GetError());
		success = false;
	} else {
		//Set starting prompt 
		gPromptTexture.loadFromRenderedText("Select your recording device:", gTextColor);

		//Get capture device count
		gRecordingDeviceCount:extern = SDL_GetNumAudioDevices(SDL_TRUE);

		//No recording devices
		if (gRecordingDeviceCount < 1) {
			Printf("Unable to get audio capture device! SDL Error: %s\n", SDL_GetError());
			success = false;
		} else {
			//At least one device connected
			if (gRecordingDeviceCount > MAX_RECORDING_DEVICES) {
				//Cap recording device count
				gRecordingDeviceCount = MAX_RECORDING_DEVICES;
			}

			//Render device names
			repeat (gRecordingDeviceCount) {|i|
				//Get capture device name
				promptText = Format("%d: %s", i, SDL_GetAudioDeviceName(i, SDL_TRUE));

				//Set texture from name
				gDeviceTextures[i].loadFromRenderedText(promptText, gTextColor);
			}
		}
	}

	success;
}

close() = {
	//Free textures
	gPromptTexture.free();
	repeat (MAX_RECORDING_DEVICES) {|i|
		gDeviceTextures[i].free();
	}

	//Free global font
	TTF_CloseFont(gFont);
	gFont:extern = nil;

	//Destroy window
	SDL_DestroyRenderer(gRenderer);
	SDL_DestroyWindow(gWindow);
	gWindow:extern = nil;
	gRenderer:extern = nil;

	//Quit SDL subsystems
	TTF_Quit();
	IMG_Quit();
	SDL_Quit();
}

audioRecordingCallback(userdata, stream as Pointer) = {
	//Copy audio from stream
	//memcpy(&gRecordingBuffer[userdata.bufferBytePosition], stream, len);
	len = stream.bytesAvail
	if (userdata.bufferBytePosition + len < gBufferByteMaxPosition) {
		(gRecordingBuffer.p + userdata.bufferBytePosition).Append(stream)
	}
	//Move along buffer
	userdata.bufferBytePosition += len;
	//Printf('Sub: %s\n', userdata.__id__)
}

audioPlaybackCallback(userdata, stream as Pointer) = {
	//Copy audio to stream
	//memcpy(stream, &gRecordingBuffer[userdata.bufferBytePosition], len);
	len = stream.bytesAvail
	stream.Append(gRecordingBuffer.p + userdata.bufferBytePosition, len)

	//Move along buffer
	userdata.bufferBytePosition += len;
}

main() = {
	//Start up SDL and create window
	if (!init()) {
		Printf("Failed to initialize!\n");
	} else {
		//Load media
		if (!loadMedia()) {
			Printf("Failed to load media!\n");
		} else {
			userdata = UserData()

			//Main loop flag
			quit = false

			//Event handler
			e = SDL_Event()

			//Set the default recording state
			currentState = `SELECTING_DEVICE;

			//Audio device IDs
			recordingDeviceId = 0;
			playbackDeviceId = 0;

			//While application is running
			while (!quit) {
				//The rerender text flag
				renderText = false;

				//Handle events on queue
				while (SDL_PollEvent(e) != 0) {
					//User requests quit
					if (e.type == SDL_QUIT) {
						quit = true;
					}

					//Do current state event handling
					if (currentState == `SELECTING_DEVICE) {
						//User is selecting recording device

						//On key press
						if (e.type == SDL_KEYDOWN) {
							//Handle key press from 0 to 9 
							if (e.key.keysym.sym >= SDLK_0 && e.key.keysym.sym <= SDLK_9) {
								//Get selection index
								index = e.key.keysym.sym - SDLK_0;

								//Index is valid
								if (index < gRecordingDeviceCount) {
									//Default audio spec
									desiredRecordingSpec = SDL_AudioSpec();
									desiredRecordingSpec.freq = 44100;
									desiredRecordingSpec.format = AUDIO_F32;
									desiredRecordingSpec.channels = 2;
									desiredRecordingSpec.samples = 4096;
									desiredRecordingSpec.callback = audioRecordingCallback;
									desiredRecordingSpec.userdata = userdata

									//Open recording device
									recordingDeviceId = SDL_OpenAudioDevice(SDL_GetAudioDeviceName(index, SDL_TRUE),
										SDL_TRUE, desiredRecordingSpec, gReceivedRecordingSpec, SDL_AUDIO_ALLOW_FORMAT_CHANGE);
									
									if (recordingDeviceId == 0) {
										//Device failed to open
										//Report error
										Printf("Failed to open recording device! SDL Error: %s", SDL_GetError());
										gPromptTexture.loadFromRenderedText("Failed to open recording device!", gTextColor);
										currentState = `ERROR;
									} else {
										//Device opened successfully
										//Default audio spec
										desiredPlaybackSpec = SDL_AudioSpec();
										desiredPlaybackSpec.freq = 44100;
										desiredPlaybackSpec.format = AUDIO_F32;
										desiredPlaybackSpec.channels = 2;
										desiredPlaybackSpec.samples = 4096;
										desiredPlaybackSpec.callback = audioPlaybackCallback;
										desiredPlaybackSpec.userdata = userdata

										//Open playback device
										playbackDeviceId = SDL_OpenAudioDevice(nil,
											SDL_FALSE, desiredPlaybackSpec, gReceivedPlaybackSpec, SDL_AUDIO_ALLOW_FORMAT_CHANGE);

										if (playbackDeviceId == 0) {
											//Device failed to open
											//Report error
											Printf("Failed to open playback device! SDL Error: %s", SDL_GetError());
											gPromptTexture.loadFromRenderedText("Failed to open playback device!", gTextColor);
											currentState = `ERROR;
										} else {
											//Device opened successfully
											//Calculate per sample bytes
											bytesPerSample = gReceivedRecordingSpec.channels * (SDL_AUDIO_BITSIZE(gReceivedRecordingSpec.format) / 8);

											//Calculate bytes per second
											bytesPerSecond = gReceivedRecordingSpec.freq * bytesPerSample;

											//Calculate buffer size
											gBufferByteSize:extern = RECORDING_BUFFER_SECONDS * bytesPerSecond;

											//Calculate max buffer use
											gBufferByteMaxPosition:extern = MAX_RECORDING_SECONDS * bytesPerSecond;

											//Allocate and initialize byte buffer
											gRecordingBuffer:extern = Array(`uint8, gBufferByteSize);

											//Go on to next state
											gPromptTexture.loadFromRenderedText("Press 1 to record for 5 seconds.", gTextColor);
											currentState = `STOPPED;
										}
									}
								}
							}
						}
					} elsif (currentState == `STOPPED) {
						//User getting ready to record

						//On key press
						if (e.type == SDL_KEYDOWN) {
							//Start recording
							if (e.key.keysym.sym == SDLK_1) {
								//Go back to beginning of buffer
								userdata.bufferBytePosition = 0;

								//Start recording
								SDL_PauseAudioDevice(recordingDeviceId, SDL_FALSE);

								//Go on to next state
								gPromptTexture.loadFromRenderedText("Recording...", gTextColor);
								currentState = `RECORDING;
							}
						}
					} elsif (currentState == `RECORDED) {
						//User has finished recording
						//On key press
						if (e.type == SDL_KEYDOWN) {
							//Start playback
							if (e.key.keysym.sym == SDLK_1) {
								//Go back to beginning of buffer
								userdata.bufferBytePosition = 0;

								//Start playback
								SDL_PauseAudioDevice(playbackDeviceId, SDL_FALSE);

								//Go on to next state
								gPromptTexture.loadFromRenderedText("Playing...", gTextColor);
								currentState = `PLAYBACK;
							}
							//Record again
							if (e.key.keysym.sym == SDLK_2) {
								//Reset the buffer
								userdata.bufferBytePosition = 0;
								//memset(gRecordingBuffer, 0, gBufferByteSize);

								//Start recording
								SDL_PauseAudioDevice(recordingDeviceId, SDL_FALSE);

								//Go on to next state
								gPromptTexture.loadFromRenderedText("Recording...", gTextColor);
								currentState = `RECORDING;
							}
						}
					}
				}

				//Updating recording
				if (currentState == `RECORDING) {
					Printf('%d\n', userdata.bufferBytePosition)
					//Printf('Main:%s\n', desiredRecordingSpec.userdata.__id__)
					//Lock callback
					SDL_LockAudioDevice(recordingDeviceId);

					//Finished recording
					if (userdata.bufferBytePosition > gBufferByteMaxPosition) {
						//Stop recording audio
						SDL_PauseAudioDevice(recordingDeviceId, SDL_TRUE);

						//Go on to next state
						gPromptTexture.loadFromRenderedText("Press 1 to play back. Press 2 to record again.", gTextColor);
						currentState = `RECORDED;
					}

					//Unlock callback
					SDL_UnlockAudioDevice(recordingDeviceId);
				} elsif (currentState == `PLAYBACK) {
					//Updating playback
					//Lock callback
					SDL_LockAudioDevice(playbackDeviceId);

					//Finished playback
					if (userdata.bufferBytePosition > gBufferByteMaxPosition) {
						//Stop playing audio
						SDL_PauseAudioDevice(playbackDeviceId, SDL_TRUE);

						//Go on to next state
						gPromptTexture.loadFromRenderedText("Press 1 to play back. Press 2 to record again.", gTextColor);
						currentState = `RECORDED;
					}

					//Unlock callback
					SDL_UnlockAudioDevice(playbackDeviceId);
				}

				//Clear screen
				SDL_SetRenderDrawColor(gRenderer, 0xFF, 0xFF, 0xFF, 0xFF);
				SDL_RenderClear(gRenderer);

				//Render prompt centered at the top of the screen
				gPromptTexture.render((SCREEN_WIDTH - gPromptTexture.getWidth()) / 2, 0);

				//User is selecting 
				if (currentState == `SELECTING_DEVICE) {
					//Render device names
					yOffset = gPromptTexture.getHeight() * 2;
					repeat (gRecordingDeviceCount) {|i|
						gDeviceTextures[i].render(0, yOffset);
						yOffset += gDeviceTextures[i].getHeight() + 1;
					}
				}

				//Update screen
				SDL_RenderPresent(gRenderer);
			}
		}
	}

	//Free resources and close SDL
	close();
}

main()
