#!/usr/bin/env gurax
import(sdl) {*}

Texture = class {
	wd as Number
	ht as Number
	entity as SDL_Texture
	__init__(renderer as SDL_Renderer, surface as SDL_Surface) = {
		this.wd = surface.w, this.ht = surface.h
		this.entity = renderer.CreateTextureFromSurface(surface)
		surface.Free()
	}
	__del__() = {
		this.entity.Destroy()
	}
}

SDL_Init(SDL_INIT_EVERYTHING)
TTF_Init()
[wdWindow, htWindow] = [800, 600]
window = SDL_CreateWindow(path.FileName(sys.argv[0]), SDL_WINDOWPOS_CENTERED, SDL_WINDOWPOS_CENTERED, wdWindow, htWindow, 0)
renderer = SDL_CreateRenderer(window, -1, SDL_RENDERER_ACCELERATED | SDL_RENDERER_PRESENTVSYNC)
font = TTF_OpenFont(path.Join(sys.dirBase, 'sample/resource/LeroyLetteringLightBeta01.ttf'), 30)
event = SDL_Event()
i = 0
repeat {
	renderer.SetDrawColor(240, 255, 240, 255)
	renderer.Clear()
	Texture(renderer, font.RenderText_Solid('Solid', (255, 0, 0, 128))) {|texture|
		renderer.Copy(texture.entity, nil, SDL_Rect(0, 0, texture.wd, texture.ht))
	}
	Texture(renderer, font.RenderText_Shaded('Shaded', (255, 0, 0, 128), (0, 255, 0, 0))) {|texture|
		renderer.Copy(texture.entity, nil, SDL_Rect(0, 40, texture.wd, texture.ht))
	}
	Texture(renderer, font.RenderText_Blended('Blended', (255, 0, 0, 128))) {|texture|
		renderer.Copy(texture.entity, nil, SDL_Rect(0, 80, texture.wd, texture.ht))
	}
	i += 1
	rtn = SDL_WaitEventTimeout(event, 10) // 10msec
	if (rtn == 0) {
		// nothing to do
	} elsif (event.type == SDL_KEYDOWN) {
		(event.key.keysym.sym == SDLK_ESCAPE) && break
	} elsif (event.type == SDL_QUIT) {
		break
	}
	renderer.Present()
}
renderer.Destroy()
window.Destroy()
SDL_Quit()
