#!/usr/bin/env gurax
import(sdl) {*}
import(jpeg)
import(gif)

I(fileName as String) = path.Join('../../resource', fileName)

Chicken = struct {
	x as Number
	y as Number
	wd as Number
	dir:nil as Symbol
	moveFlag as Bool
	texturesL as Iterator
	texturesR as Iterator
	Render(renderer as SDL_Renderer):void = {
		texture = cond(this.dir == `left, this.texturesL, this.texturesR).NextValue()
		ht = texture.ht * this.wd / texture.wd
		renderer.Copy(texture.entity, nil, SDL_Rect(this.x, this.y - ht, this.wd, ht))
	}
	Move(wdScreen as Number):void = {
		xStep = this.wd * .2
		if (this.moveFlag) {
			this.x += cond(this.dir == `left, -xStep, xStep)
			if (this.x + this.wd > wdScreen) {
				this.dir = `left
			} elsif (this.x <= 0) {
				this.dir = `right
			}
		}
	}
}

Texture = class {
	entity as SDL_Texture
	wd as Number
	ht as Number
	__init__(renderer as SDL_Renderer, surface as SDL_Surface):map = {
		this.entity = renderer.CreateTextureFromSurface(surface)
		this.wd = surface.w
		this.ht = surface.h
		//SDL_FreeSurface(surface)
		surface.Free()
	}
}

AddChicken(x as Number, y as Number, wd as Number, dir as Symbol = `right, moveFlag as Bool = false) = {
	chickens.Add(Chicken(x, y, wd, dir, moveFlag, texturesChickenL.Cycle(), texturesChickenR.Cycle()))
}

SDL_Init(SDL_INIT_EVERYTHING)
[wdScreen, htScreen] = [800, 600]
window = SDL_CreateWindow(path.FileName(sys.argv[0]), SDL_WINDOWPOS_CENTERED, SDL_WINDOWPOS_CENTERED, wdScreen, htScreen, 0)
renderer = SDL_CreateRenderer(window, -1, SDL_RENDERER_ACCELERATED | SDL_RENDERER_PRESENTVSYNC)
textureBg = Texture(renderer, Image(I('seashore.jpg')))
imgs = gif.Content(I('chicken-anim.gif')):*EachImage():list
texturesChickenL = Texture(renderer, imgs):list
texturesChickenR = Texture(renderer, imgs:*Flip(`horz)):list
wdInit = 40
chickens = []
AddChicken(10, 100, 80, `right, true)
timePrev = SDL_GetTicks()
event = SDL_Event()
iSize = 0
repeat {
	timeCur = SDL_GetTicks()
	if (timePrev > timeCur || timeCur > timePrev + 100) {
		renderer.Copy(textureBg.entity, nil, nil)
		chickens:*Render(renderer)
		chickens:*Move(wdScreen)
		renderer.Present()
		if (!chickenLast.moveFlag) {
			chickenLast.wd = Min(iSize * 10 + wdInit, 400)
			iSize += 1
		}
		timePrev = timeCur
	}
	rtn = SDL_WaitEventTimeout(event, 10) // 10msec
	chickenLast = chickens.last
	if (rtn == 0) {
		// nothing to do
	} elsif (event.type == SDL_QUIT) {
		break
	} elsif (event.type == SDL_KEYDOWN) {
		(event.key.keysym.sym == SDLK_ESCAPE) && break
	} elsif (event.type == SDL_MOUSEBUTTONDOWN) {
		//Println('down')
		iSize = 0
		if (event.button.button == SDL_BUTTON_LEFT) {
			AddChicken(event.button.x, event.button.y, wdInit, `left)
		} elsif (event.button.button == SDL_BUTTON_RIGHT) {
			AddChicken(event.button.x, event.button.y, wdInit, `right)
		}
	} elsif (event.type == SDL_MOUSEBUTTONUP) {
		//Println('up')
		if (!chickenLast.moveFlag) {
			chickenLast.moveFlag = true
		}
	}
}
renderer.Destroy()
window.Destroy()
SDL_Quit()
