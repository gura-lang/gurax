#!/usr/bin/env gurax
import(wx)
import(opengl) {*}
import(Samples)

Pack = struct {
	sample as Samples.Sample
	context as wx.GLContext
	initializedFlag as Bool = false
}


AppMain = class(wx.App) {
	OnInit() = {
		FrameMain(path.BaseName(path.FileName(sys.argv[0])), wx.Point(50, 50), wx.Size(800, 600)).Show()
		true
	}
}

FrameMain = class(wx.Frame) {
	canvas as Canvas
	__init__(title as String, pos as wx.Point, size as wx.Size) = {|nil, wx.ID_ANY, title, pos, size|
		PanelTop(this)
	}
}

PanelTop = class(wx.Panel) {
	canvas as Canvas
	textCtrl_Init as wx.TextCtrl
	textCtrl_Reshape as wx.TextCtrl
	textCtrl_Display as wx.TextCtrl
	packs[] as Pack
	__init__(parent as wx.Window) = {|parent|
		sizerTop = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(sizerTop)
		wx.BoxSizer(wx.HORIZONTAL) {|hbox|
			sizerTop.Add(hbox, wx.SizerFlags(1).Expand())
			choices = Samples.samples:*name
			wx.ListBox(this, wx.ID_ANY, choices = choices) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags(1).Expand())
				wx.EVT_LISTBOX(ctrl, this.OnListBox)
				ctrl.SetSelection(0)
			}
			wx.BoxSizer(wx.VERTICAL) {|vbox|
				hbox.Add(vbox, wx.SizerFlags(3).Expand())
				Canvas(this) {|ctrl|
					vbox.Add(ctrl, wx.SizerFlags().Align(wx.CENTER))
					this.canvas = ctrl
				}
				wx.Notebook(this, wx.ID_ANY, style = wx.NB_FIXEDWIDTH) {|book|
					vbox.Add(book, wx.SizerFlags(1).Expand().Border(wx.TOP, 4))
					[this.textCtrl_Init, this.textCtrl_Reshape, this.textCtrl_Display] = \
					['Init', 'Reshape', 'Display'].Each:list {|label|
						ctrl = wx.TextCtrl(book, wx.ID_ANY, style = wx.TE_MULTILINE)
						book.AddPage(ctrl, label)
						ctrl.SetFont(wx.Font(12, wx.FONTFAMILY_TELETYPE,  wx.FONTSTYLE_NORMAL, wx.FONTWEIGHT_NORMAL))
						ctrl
					}
					book.SetSelection(2)
				}
			}
		}
		this.packs = Samples.samples.Each:list {|sample| Pack(sample, wx.GLContext(this.canvas))}
		this.SetPack(this.packs[0])
	}
	SetPack(pack as Pack) = {
		this.canvas.SetPack(pack)
		this.canvas.Refresh()
		this.textCtrl_Init.SetValue(pack.sample.Init.expr.EachElem():*Textize('    ').Join('\n'))
		this.textCtrl_Reshape.SetValue(pack.sample.Reshape.expr.EachElem():*Textize('    ').Join('\n'))
		this.textCtrl_Display.SetValue(pack.sample.Display.expr.EachElem():*Textize('    ').Join('\n'))
	}
	OnListBox(event as wx.CommandEvent) = {
		this.SetPack(this.packs[event.GetSelection()])
	}
}

Canvas = class(wx.GLCanvas) {
	pack as Pack
	__init__(parent as wx.Window) = {|parent, wx.ID_ANY, size = wx.Size(400, 300)|
		this.Bind(wx.EVT_SIZE, this.OnSize)
		this.Bind(wx.EVT_PAINT, this.OnPaint)
	}
	SetPack(pack as Pack) = {
		size = this.GetClientSize()
		this.SetCurrent(pack.context)
		if (!pack.initializedFlag) {
			pack.sample.Init()
			pack.initializedFlag = true
		}
		pack.sample.Reshape(size.GetWidth(), size.GetHeight())
		this.pack = pack
		this.Refresh()
	}
	OnSize(event as wx.SizeEvent) = {
		size = event.GetSize()
		this.pack && this.pack.sample.Reshape(size.GetWidth(), size.GetHeight())
	}
	OnPaint(event as wx.PaintEvent) = {
		dc = wx.PaintDC(this)
		if (this.pack) {
			this.SetCurrent(this.pack.context)
			this.pack.sample.Display()
			this.SwapBuffers()
		}
		dc = nil
	}
}

wx.ImplementApp(AppMain())
