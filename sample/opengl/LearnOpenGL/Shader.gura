#!/usr/bin/env gurax
import(opengl) {*}

Shader = class {
	ID as Number
	// constructor generates the shader on the fly
	// ------------------------------------------------------------------------
	__init__(vertexStream as Stream, fragmentStream as Stream) = {
		// convert stream into string
		vertexCode = vertexStream.ReadText();
		fragmentCode = fragmentStream.ReadText();
		// vertex shader
		shaderVertex = glCreateShader(GL_VERTEX_SHADER);
		this.CompileShader(shaderVertex, vertexCode, 'VERTEX')
		// fragment Shader
		shaderFragment = glCreateShader(GL_FRAGMENT_SHADER);
		this.CompileShader(shaderFragment, fragmentCode, 'FRAGMENT')
		// shader Program
		this.ID = glCreateProgram();
		glAttachShader(this.ID, shaderVertex);
		glAttachShader(this.ID, shaderFragment);
		glLinkProgram(this.ID);
		success = @int {|v| glGetProgramiv(this.ID, GL_LINK_STATUS, v), v.@}
		if (success == 0) {
			infoLog = @char(1024)
			glGetProgramInfoLog(this.ID, infoLog.bytes, nil, infoLog);
			Println("ERROR::PRPGRAM_LINKING_ERROR of type: ", 'PROGRAM')
			Println(infoLog)
			Println("-- --------------------------------------------------- -- ")
		}
		// delete the shaders as they're linked into our ID now and no longer necessary
		glDeleteShader(shaderVertex);
		glDeleteShader(shaderFragment);
	}
	// activate the shader
	// ------------------------------------------------------------------------
	use() = {
		glUseProgram(this.ID)
	}
	// utility uniform functions
	// ------------------------------------------------------------------------
	setBool(name as String, value as Bool) = {         
		glUniform1i(glGetUniformLocation(this.ID, name), value) 
	}
	// ------------------------------------------------------------------------
	setInt(name as String, value as Number) = {
		glUniform1i(glGetUniformLocation(this.ID, name), value) 
	}
	// ------------------------------------------------------------------------
	setFloat(name as String, value as Number) = {
		glUniform1f(glGetUniformLocation(this.ID, name), value) 
	}
	// ------------------------------------------------------------------------
	setVec2(name as String, x as Number, y as Number) = {
		uniformLoc = glGetUniformLocation(this.ID, name)
		glUniform2f(uniformLoc, x, y)
	}
	// ------------------------------------------------------------------------
	setVec3(name as String, x as Number, y as Number, z as Number) = {
		uniformLoc = glGetUniformLocation(this.ID, name)
		glUniform3f(uniformLoc, x, y, z)
	}
	// ------------------------------------------------------------------------
	setVec4(name as String, x as Number, y as Number, z as Number, w as Number) = {
		uniformLoc = glGetUniformLocation(this.ID, name)
		glUniform4f(uniformLoc, x, y, z, w)
	}
	// ------------------------------------------------------------------------
	setArray(name as String, array as Array) = {
		uniformLoc = glGetUniformLocation(this.ID, name)
		if (array.elemType == `float) {
			if (array.VerifyShape(2)) {
				glUniform2fv(uniformLoc, 1, array.p)
			} elsif (array.VerifyShape(3)) {
				glUniform3fv(uniformLoc, 1, array.p)
			} elsif (array.VerifyShape(4)) {
				glUniform4fv(uniformLoc, 1, array.p)
			} elsif (array.VerifyShape(2, 2)) {
				glUniformMatrix2fv(uniformLoc, 1, GL_TRUE, array.p)
			} elsif (array.VerifyShape(3, 3)) {
				glUniformMatrix3fv(uniformLoc, 1, GL_TRUE, array.p)
			} elsif (array.VerifyShape(4, 4)) {
				glUniformMatrix4fv(uniformLoc, 1, GL_TRUE, array.p)
			}
		}
	}
	CompileShader(shader as Number, shaderCode as String, typeName as String) = {
		glShaderSource(shader, [shaderCode], []);
		glCompileShader(shader);
		success = @int {|v| glGetShaderiv(shader, GL_COMPILE_STATUS, v), v.@}
		if (success == 0) {
			infoLog = @char(1024)
			glGetShaderInfoLog(shader, infoLog.bytes, nil, infoLog);
			Println("ERROR::SHADER_COMPILATION_ERROR of type: ", typeName)
			Println(infoLog)
			Println("-- --------------------------------------------------- -- ")
		}
	}
}
