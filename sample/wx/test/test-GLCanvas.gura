#!/usr/bin/env gurax
import(wx)
import(opengl) {*}

glewInit()

AppMain = class(wx.App) {
	OnInit() = {
		FrameMain(path.BaseName(path.FileName(sys.argv[0])), wx.Point(50, 50), wx.Size(800, 600)).Show()
		true
	}
}

FrameMain = class(wx.Frame) {
	__init__(title as String, pos as wx.Point, size as wx.Size) = {|nil, wx.ID_ANY, title, pos, size|
		GLCanvas(this)
	}
}

Display() = {
	glClearColor(0.3, 0.4, 0.6, 1.0)
	glClear(GL_COLOR_BUFFER_BIT | GL_DEPTH_BUFFER_BIT)
	glBegin(GL_POLYGON)
		glColor3f(1, 0, 0); glVertex3f(-0.6, -0.75, 0.5)
		glColor3f(0, 1, 0); glVertex3f(0.6, -0.75, 0)
		glColor3f(0, 0, 1); glVertex3f(0, 0.75, 0)
	glEnd()
	glFlush()
}

GLCanvas = class(wx.GLCanvas) {
	m_context as wx.GLContext
	__init__(parent as wx.Window) = {|parent, wx.ID_ANY|
		this.m_context = wx.GLContext(this)
		this.Bind(wx.EVT_PAINT, this.OnPaint)
	}
	OnPaint(event as wx.PaintEvent) = {
		dc = wx.PaintDC(this)
		this.SetCurrent(this.m_context)
		Display()
		this.SwapBuffers()
		dc = nil
	}
}

wx.ImplementApp(AppMain())
