#!/usr/bin/env gurax
import(wx)

MyApp = class(wx.App) {
	OnInit() = {
		MyFrame(path.BaseName(path.FileName(sys.argv[0])), wx.Point(50, 50), wx.Size(800, 600)).Show()
		true
	}
}

MyFrame = class(wx.Frame) {
	__init__(title as String, pos as wx.Point, size as wx.Size) = {|nil, wx.ID_ANY, title, pos, size|
		PanelTop(this)
	}
}

PanelTop = class(wx.Panel) {
	textCtrl as wx.TextCtrl
	__init__(parent as wx.Window) = {|parent|
		sizerTop = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(sizerTop)
		//choices = [
		//	'<a href="hoge">fdas</a>fas'
		//	'<h1>Item <b>1</b></h1>'
		//]
		wx.BoxSizer(wx.VERTICAL) {|vbox|
			sizerTop.Add(vbox, wx.SizerFlags(1).Expand())
			wx.SimpleHtmlListBox(this, wx.ID_ANY, wx.DefaultPosition, wx.DefaultSize, []) {|ctrl|
				Range(100).Each {|iItem|
					level = (iItem % 6) + 1
					ctrl.Append(Format('<h%d>Item %d <a href="link-href">Link</a></h%d>', level, iItem, level))
				}
				vbox.Add(ctrl, wx.SizerFlags(3).Expand())
				wx.EVT_LISTBOX(ctrl, this.OnListBox)
				wx.EVT_LISTBOX_DCLICK(ctrl, this.OnListBoxDClick)
				wx.EVT_HTML_CELL_CLICKED(ctrl, this.OnHtmlCellClicked)
				wx.EVT_HTML_CELL_HOVER(ctrl, this.OnHtmlCellHover)
				wx.EVT_HTML_LINK_CLICKED(ctrl, this.OnHtmlLinkClicked)
			}
		}
		wx.TextCtrl(this, wx.ID_ANY, style = wx.TE_MULTILINE) {|ctrl|
			sizerTop.Add(ctrl, wx.SizerFlags(1).Expand().Border(wx.TOP, 2))
			this.textCtrl = ctrl
		}
	}
	OnListBox(event as wx.CommandEvent) = {
		this.Printf('OnListBox index=%d\n', event.GetSelection())
	}
	OnListBoxDClick(event as wx.CommandEvent) = {
		this.Printf('OnListBoxDClick index=%d\n', event.GetSelection())
	}
	OnHtmlCellClicked(event as wx.HtmlCellEvent) = {
		this.Printf('OnHtmlCellClicked %s\n', event)
		event.Skip()
	}
	OnHtmlCellHover(event as wx.HtmlCellEvent) = {
		this.Printf('OnHtmlCellHover %s\n', event)
		event.Skip()
	}
	OnHtmlLinkClicked(event as wx.HtmlLinkEvent) = {
		linkInfo = event.GetLinkInfo()
		this.Printf('OnHtmlLinkClicked target="%s" href="%s"\n', linkInfo.GetTarget(), linkInfo.GetHref())
		event.Skip()
	}
	Printf(format as String, args*) = this.textCtrl.WriteText(Format(format, args*))
}

wx.ImplementApp(MyApp())
