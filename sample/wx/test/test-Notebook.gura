#!/usr/bin/env gurax
import(wx)

AppMain = class(wx.App) {
	OnInit() = {
		FrameMain(path.BaseName(path.FileName(sys.argv[0])), wx.Point(50, 50), wx.Size(800, 600)).Show()
		true
	}
}

FrameMain = class(wx.Frame) {
	__init__(title as String, pos as wx.Point, size as wx.Size) = {|nil, wx.ID_ANY, title, pos, size|
		PanelTop(this)
	}
}

PanelTop = class(wx.Panel) {
	textCtrl as wx.TextCtrl
	__init__(parent as wx.Window) = {|parent|
		sizerTop = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(sizerTop)
		wx.BoxSizer(wx.HORIZONTAL) {|hbox|
			sizerTop.Add(hbox, wx.SizerFlags(1).Expand())
			wx.Notebook(this, wx.ID_ANY, wx.DefaultPosition, wx.DefaultSize, wx.NB_TOP) {|notebook|
				hbox.Add(notebook, wx.SizerFlags(1).Expand())
				this.SetupNotebook(notebook, 'NB_TOP')
			}
			wx.Notebook(this, wx.ID_ANY, wx.DefaultPosition, wx.DefaultSize, wx.NB_BOTTOM) {|notebook|
				hbox.Add(notebook, wx.SizerFlags(1).Expand())
				this.SetupNotebook(notebook, 'NB_BOTTOM')
			}
		}
		wx.BoxSizer(wx.HORIZONTAL) {|hbox|
			sizerTop.Add(hbox, wx.SizerFlags(1).Expand())
			wx.Notebook(this, wx.ID_ANY, wx.DefaultPosition, wx.DefaultSize, wx.NB_LEFT) {|notebook|
				hbox.Add(notebook, wx.SizerFlags(1).Expand())
				this.SetupNotebook(notebook, 'NB_LEFT')
			}
			wx.Notebook(this, wx.ID_ANY, wx.DefaultPosition, wx.DefaultSize, wx.NB_RIGHT) {|notebook|
				hbox.Add(notebook, wx.SizerFlags(1).Expand())
				this.SetupNotebook(notebook, 'NB_RIGHT')
			}
		}
		wx.TextCtrl(this, wx.ID_ANY, '', style = wx.TE_MULTILINE) {|ctrl|
			ctrl.SetFont(wx.Font(12, wx.FONTFAMILY_TELETYPE,  wx.FONTSTYLE_NORMAL, wx.FONTWEIGHT_NORMAL))
			sizerTop.Add(ctrl, wx.SizerFlags(1).Expand())
			this.textCtrl = ctrl
		}
	}
	SetupNotebook(notebook as wx.Notebook, name as String) = {
		//notebook.SetPadding(wx.Size(4, 4))
		['Page1', 'Page2', 'Page3', 'Page4'].Each {|label|
			wx.TextCtrl(notebook, wx.ID_ANY, Format('%s - %s', name, label)) {|ctrl|
				notebook.AddPage(ctrl, label)
			}
		}
		notebook.Bind(wx.EVT_NOTEBOOK_PAGE_CHANGING, this.OnNotebookPageChanging, userData = name)
		notebook.Bind(wx.EVT_NOTEBOOK_PAGE_CHANGED, this.OnNotebookPageChanged, userData = name)
	}
	OnNotebookPageChanging(event as wx.BookCtrlEvent) = {
		this.Printf('NotebookPageChanging(%s)\n', event.GetEventUserData())
	}
	OnNotebookPageChanged(event as wx.BookCtrlEvent) = {
		this.Printf('NotebookPageChanged(%s)\n', event.GetEventUserData())
	}
	Printf(format as String, args*) = {
		this.textCtrl.AppendText(Format(format, args*))
	}
}

wx.ImplementApp(AppMain())
