#!/usr/bin/env gurax
import(wx)

textCtrl as wx.TextCtrl = nil

LogPrintf(format as String, args*) = {
	textCtrl.AppendText(Format(format, args*))
}

AppMain = class(wx.App) {
	OnInit() = {
		FrameMain(path.BaseName(path.FileName(sys.argv[0])), wx.Point(50, 50), wx.Size(800, 600)).Show()
		true
	}
}

FrameMain = class(wx.Frame) {
	__init__(title as String, pos as wx.Point, size as wx.Size) = {|nil, wx.ID_ANY, title, pos, size|
		PanelTop(this)
	}
}

panels = []

Panel = class(wx.Panel) {
	name as String
	vbox as wx.BoxSizer
	__init__(parent as wx.Window, name as String) = {|parent|
		this.name = name
		sizerTop = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(sizerTop)
		wx.Button(this, wx.ID_ANY, 'Show ' + this.name) {|ctrl|
			sizerTop.Add(ctrl, wx.SizerFlags().Expand().Border(wx.ALL, 8))
			ctrl.Bind(wx.EVT_BUTTON, this.OnButton)
		}
		this.vbox = wx.BoxSizer(wx.VERTICAL)
		sizerTop.Add(this.vbox, wx.SizerFlags(1).Expand().Border(wx.ALL, 8))
	}
	OnButton(event as wx.CommandEvent) = this.ShowDialog()
}

PanelTop = class(wx.Panel) {
	__init__(parent as wx.Window) = {|parent|
		sizerTop = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(sizerTop)
		wx.Notebook(this, wx.ID_ANY) {|notebook|
			sizerTop.Add(notebook, wx.SizerFlags(1).Expand())
			panels.Each {|Panel|
				Panel(notebook) {|panel| notebook.AddPage(panel, panel.name)}
			}
			//notebook.SetSelection(2)
		}
		wx.TextCtrl(this, wx.ID_ANY, '', style = wx.TE_MULTILINE) {|ctrl|
			ctrl.SetFont(wx.Font(12, wx.FONTFAMILY_TELETYPE,  wx.FONTSTYLE_NORMAL, wx.FONTWEIGHT_NORMAL))
			textCtrl:extern = ctrl
			sizerTop.Add(ctrl, wx.SizerFlags(1).Expand())
		}
	}
}

//------------------------------------------------------------------------------
// Panel_ColourDialog
//------------------------------------------------------------------------------
Panel_ColourDialog = class(Panel) {
	checkBox_ChooseFull
	__init__(parent as wx.Window) = {|parent, 'ColourDialog'|
		wx.CheckBox(this, wx.ID_ANY, 'ChooseFull') {|ctrl|
			this.vbox.Add(ctrl, wx.SizerFlags().Expand())
			this.checkBox_ChooseFull = ctrl
		}
	}
	ShowDialog() = {
		data = wx.ColourData()
		repeat (16) {|i|
			data.SetCustomColour(i, wx.Colour(i * 16, i * 16, i * 16))
		}
		data.SetChooseFull(this.checkBox_ChooseFull.IsChecked())
		dlg = wx.ColourDialog(
				parent = this
				data = data)
		rtn = dlg.ShowModal()
		LogPrintf('rtn = %d\n', rtn)
		if (rtn == wx.ID_OK) {
			colourData = dlg.GetColourData()
			LogPrintf('ToString():  %s\n', colourData.ToString())
			LogPrintf('GetColour(): %s\n', colourData.GetColour())
		}
	}
}
panels.Add(Panel_ColourDialog)

//------------------------------------------------------------------------------
// Panel_DirDialog
//------------------------------------------------------------------------------
Panel_DirDialog = class(Panel) {
	checkBox_DIR_MUST_EXIST
	checkBox_CHANGE_DIR
	styleTbl = []
	__init__(parent as wx.Window) = {|parent, 'DirDialog'|
		wx.CheckBox(this, wx.ID_ANY, 'DD_DIR_MUST_EXIST') {|ctrl|
			this.vbox.Add(ctrl, wx.SizerFlags().Expand())
			this.checkBox_DIR_MUST_EXIST = ctrl
		}
		wx.CheckBox(this, wx.ID_ANY, 'DD_CHANGE_DIR') {|ctrl|
			this.vbox.Add(ctrl, wx.SizerFlags().Expand().Border(wx.TOP, 8))
			this.checkBox_CHANGE_DIR = ctrl
		}
	}
	ShowDialog() = {
		style = 0
		if (this.checkBox_DIR_MUST_EXIST.IsChecked()) { style |= wx.DD_DIR_MUST_EXIST } 
		if (this.checkBox_CHANGE_DIR.IsChecked()) { style |= wx.DD_CHANGE_DIR }
		dlg = wx.DirDialog(
				parent = this
				message = wx.DirSelectorPromptStr
				defaultPath = ''
				style = style
				pos = wx.Point(300, 50))
		rtn = dlg.ShowModal()
		LogPrintf('rtn = %d\n', rtn)
		if (rtn == wx.ID_OK) {
			LogPrintf('GetMessage: %s\n', dlg.GetMessage())
			LogPrintf('GetPath     %s\n', dlg.GetPath())
		}
	}
}
panels.Add(Panel_DirDialog)

//------------------------------------------------------------------------------
// Panel_FileDialog
//------------------------------------------------------------------------------
Panel_FileDialog = class(Panel) {
	checkBox_OPEN
	checkBox_SAVE
	checkBox_OVERWRITE_PROMPT
	checkBox_FILE_MUST_EXIST
	checkBox_MULTIPLE
	checkBox_CHANGE_DIR
	checkBox_PREVIEW
	styleTbl = []
	__init__(parent as wx.Window) = {|parent, 'FileDialog'|
		wx.CheckBox(this, wx.ID_ANY, 'FD_OPEN') {|ctrl|
			this.vbox.Add(ctrl, wx.SizerFlags().Expand())
			this.checkBox_OPEN = ctrl
		}
		wx.CheckBox(this, wx.ID_ANY, 'FD_SAVE') {|ctrl|
			this.vbox.Add(ctrl, wx.SizerFlags().Expand().Border(wx.TOP, 8))
			this.checkBox_SAVE = ctrl
		}
		wx.CheckBox(this, wx.ID_ANY, 'FD_OVERWRITE_PROMPT') {|ctrl|
			this.vbox.Add(ctrl, wx.SizerFlags().Expand().Border(wx.TOP, 8))
			this.checkBox_OVERWRITE_PROMPT = ctrl
		}
		wx.CheckBox(this, wx.ID_ANY, 'FD_FILE_MUST_EXIST') {|ctrl|
			this.vbox.Add(ctrl, wx.SizerFlags().Expand().Border(wx.TOP, 8))
			this.checkBox_FILE_MUST_EXIST = ctrl
		}
		wx.CheckBox(this, wx.ID_ANY, 'FD_MULTIPLE') {|ctrl|
			this.vbox.Add(ctrl, wx.SizerFlags().Expand().Border(wx.TOP, 8))
			this.checkBox_MULTIPLE = ctrl
		}
		wx.CheckBox(this, wx.ID_ANY, 'FD_CHANGE_DIR') {|ctrl|
			this.vbox.Add(ctrl, wx.SizerFlags().Expand().Border(wx.TOP, 8))
			this.checkBox_CHANGE_DIR = ctrl
		}
		wx.CheckBox(this, wx.ID_ANY, 'FD_PREVIEW') {|ctrl|
			this.vbox.Add(ctrl, wx.SizerFlags().Expand().Border(wx.TOP, 8))
			this.checkBox_PREVIEW = ctrl
		}
	}
	ShowDialog() = {
		style = 0
		if (this.checkBox_OPEN.IsChecked()) { style |= wx.FD_OPEN } 
		if (this.checkBox_SAVE.IsChecked()) { style |= wx.FD_SAVE } 
		if (this.checkBox_OVERWRITE_PROMPT.IsChecked()) { style |= wx.FD_OVERWRITE_PROMPT } 
		if (this.checkBox_FILE_MUST_EXIST.IsChecked()) { style |= wx.FD_FILE_MUST_EXIST } 
		if (this.checkBox_MULTIPLE.IsChecked()) { style |= wx.FD_MULTIPLE } 
		if (this.checkBox_CHANGE_DIR.IsChecked()) { style |= wx.FD_CHANGE_DIR } 
		if (this.checkBox_PREVIEW.IsChecked()) { style |= wx.FD_PREVIEW }
		dlg = wx.FileDialog(
				parent = this
				message = 'Test'
				defaultDir = ''
				defaultFile = ''
				wildcard = wx.FileSelectorDefaultWildcardStr
				style = style
				pos = wx.Point(300, 50)
				size = wx.DefaultSize)
		rtn = dlg.ShowModal()
		LogPrintf('rtn = %d\n', rtn)
		if (rtn == wx.ID_OK) {
			LogPrintf('GetCurrentlySelectedFilename: %s\n', dlg.GetCurrentlySelectedFilename())
			LogPrintf('GetDirectory   %s\n', dlg.GetDirectory())
			!(style & wx.FD_MULTIPLE) && LogPrintf('GetFilename    %s\n', dlg.GetFilename())
			LogPrintf('GetFilenames   %s\n', dlg.GetFilenames().Join('\n               '))
			LogPrintf('GetMessage     %s\n', dlg.GetMessage())
			!(style & wx.FD_MULTIPLE) && LogPrintf('GetPath        %s\n', dlg.GetPath())
			LogPrintf('GetPaths       %s\n', dlg.GetPaths().Join('\n               '))
			LogPrintf('GetWildcard    %s\n', dlg.GetWildcard())
		}
	}
}
panels.Add(Panel_FileDialog)

//------------------------------------------------------------------------------
// Panel_FindReplaceDialog
//------------------------------------------------------------------------------
Panel_FindReplaceDialog = class(Panel) {
	__init__(parent as wx.Window) = {|parent, 'FindReplaceDialog'|}
	ShowDialog() = {
		data = wx.FindReplaceData()
		data.SetFindString('aaa')
		data.SetFlags(0)
		data.SetReplaceString('bbb')
		style = 0
		dlg = wx.FindReplaceDialog(
				parent = this
				data = data
				title = 'FindReplace Test'
				style = style)
		rtn = dlg.ShowModal()
		data = dlg.GetData()
	}
}
panels.Add(Panel_FindReplaceDialog)

//------------------------------------------------------------------------------
// Panel_GenericProgressDialog
//------------------------------------------------------------------------------
Panel_GenericProgressDialog = class(Panel) {
	__init__(parent as wx.Window) = {|parent, 'GenericProgressDialog'|}
	ShowDialog() = {
		style = wx.PD_AUTO_HIDE | wx.PD_APP_MODAL
		style |= wx.PD_SMOOTH | wx.PD_CAN_ABORT | wx.PD_CAN_SKIP | wx.PD_ELAPSED_TIME | wx.PD_ESTIMATED_TIME | wx.PD_REMAINING_TIME
		dlg = wx.GenericProgressDialog(
				title = ''
				message = 'message'
				maximum = 100
				parent = nil
				style = style)
		rtn = dlg.ShowModal()
		data = dlg.GetData()
	}
}
panels.Add(Panel_GenericProgressDialog)

//------------------------------------------------------------------------------
// Panel_FontDialog
//------------------------------------------------------------------------------
Panel_FontDialog = class(Panel) {
	__init__(parent as wx.Window) = {|parent, 'FontDialog'|}
	ShowDialog() = {
		data = wx.FontData()
		data.EnableEffects(false)
		data.SetAllowSymbols(false)
		data.SetInitialFont(textCtrl.GetFont())
		dlg = wx.FontDialog(this, data)
		rtn = dlg.ShowModal()
	}
}
panels.Add(Panel_FontDialog)

//------------------------------------------------------------------------------
// Panel_MessageDialog
//------------------------------------------------------------------------------
Panel_MessageDialog = class(Panel) {
	__init__(parent as wx.Window) = {|parent, 'MessageDialog'|}
	ShowDialog() = {
		//style = wx.OK | wx.CENTRE
		style = wx.OK | wx.CENTRE
		dlg = wx.MessageDialog(
				parent = this
				message = 'Message'
				caption = 'Caption'
				style = style)
		rtn = dlg.ShowModal()
		LogPrintf('rtn = %d\n', rtn)
	}
}
panels.Add(Panel_MessageDialog)

wx.ImplementApp(AppMain())
