#!/usr/bin/env gurax
# coding: utf-8
import(wx)

text = R'''
The quick brown fox jumps over the lazy dog.
'''

MyApp = class(wx.App) {
	OnInit() = {
		MyFrame(path.BaseName(path.FileName(sys.argv[0])), wx.Point(50, 50), wx.Size(640, 300)).Show()
		true
	}
}

MyFrame = class(wx.Frame) {
	__init__(title as String, pos as wx.Point, size as wx.Size) = {|nil, wx.ID_ANY, title, pos, size|
		PanelTop(this)
	}
}

SelectionItem = struct { value, label as String }

PanelTop = class(wx.Panel) {
	textCtrl as wx.TextCtrl
	comboBox_FontFamily as wx.ComboBox
	spinCtrl_PointSize as wx.SpinCtrl
	radioBox_FontWeight as wx.RadioBox
	radioBox_FontStyle as wx.RadioBox
	checkBox_Underlined as wx.CheckBox
	checkBox_Strikethrough as wx.CheckBox
	fontFamilyTbl = @(SelectionItem) {
		{ wx.FONTFAMILY_DEFAULT,	'Default'		}
		{ wx.FONTFAMILY_DECORATIVE,	'Decorative'	}
		{ wx.FONTFAMILY_ROMAN,		'Roman'			}
		{ wx.FONTFAMILY_SCRIPT,		'Script'		}
		{ wx.FONTFAMILY_SWISS,		'Swiss'			}
		{ wx.FONTFAMILY_MODERN,		'Modern'		}
		{ wx.FONTFAMILY_TELETYPE,	'Teletype'		}
	}
	fontStyleTbl = @(SelectionItem) {
		{ wx.FONTSTYLE_NORMAL,		'Normal '		}
		{ wx.FONTSTYLE_ITALIC,		'Italic'		}
		{ wx.FONTSTYLE_SLANT,		'Slant'			}
	}
	fontWeightTbl = @(SelectionItem) {
		{ wx.FONTWEIGHT_NORMAL,		'Normal '		}
		{ wx.FONTWEIGHT_LIGHT,		'Light'			}
		{ wx.FONTWEIGHT_BOLD,		'Bold'			}
	}
	__init__(parent as wx.Window) = {|parent|
		iFontFamily = 0
		fontSize = 12
		iFontWeight = 0
		iFontStyle = 0
		underlinedFlag = false
		strikethroughFlag = false
		sizerTop = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(sizerTop)
		wx.TextCtrl(this, wx.ID_ANY, text, style = wx.TE_MULTILINE) {|ctrl|
			sizerTop.Add(ctrl, wx.SizerFlags(1).Expand())
			font = ctrl.GetFont()
			//font.SetEncoding(wx.FONTENCODING_UTF8)
			font.SetFamily(this.fontFamilyTbl[iFontFamily].value)
			font.SetPointSize(fontSize)
			font.SetWeight(this.fontWeightTbl[iFontWeight].value)
			font.SetStyle(this.fontStyleTbl[iFontStyle].value)
			ctrl.SetFont(font)
			this.textCtrl = ctrl
		}
		wx.BoxSizer(wx.VERTICAL) {|vbox|
			sizerTop.Add(vbox, wx.SizerFlags().Expand().Border(wx.ALL, 8))
			wx.BoxSizer(wx.HORIZONTAL) {|hbox|
				vbox.Add(hbox, wx.SizerFlags().Expand())
				hbox.Add(wx.StaticText(this, wx.ID_ANY, label = 'Font &Family'),  wx.SizerFlags().Center())
				wx.ComboBox(this, wx.ID_ANY, choices = this.fontFamilyTbl::label, style = wx.CB_READONLY) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Center().Border(wx.LEFT, 8))
					ctrl.SetSelection(iFontFamily)
					ctrl.Bind(wx.EVT_COMBOBOX, this.OnEvent)
					this.comboBox_FontFamily = ctrl
				}
				hbox.Add(wx.StaticText(this, wx.ID_ANY, label = '&Point Size'),  wx.SizerFlags().Center().Border(wx.LEFT, 16))
				wx.SpinCtrl(this, min = 1, max = 100, initial = fontSize, style = wx.SP_ARROW_KEYS | wx.TE_READONLY) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Center().Border(wx.LEFT, 8))
					ctrl.Bind(wx.EVT_SPINCTRL, this.OnEvent)
					this.spinCtrl_PointSize = ctrl
				}
				wx.CheckBox(this, wx.ID_ANY, '&Underlined') {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Center().Border(wx.LEFT, 16))
					ctrl.SetValue(underlinedFlag)
					ctrl.Bind(wx.EVT_CHECKBOX, this.OnEvent)
					this.checkBox_Underlined = ctrl
				}
				wx.CheckBox(this, wx.ID_ANY, 'S&trikethrough') {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Center().Border(wx.LEFT, 16))
					ctrl.SetValue(strikethroughFlag)
					ctrl.Bind(wx.EVT_CHECKBOX, this.OnEvent)
					this.checkBox_Strikethrough = ctrl
				}
			}
			wx.BoxSizer(wx.HORIZONTAL) {|hbox|
				vbox.Add(hbox, wx.SizerFlags().Expand())
				hbox.Add(wx.StaticText(this, wx.ID_ANY, label = 'Font &Style'),  wx.SizerFlags().Center())
				wx.RadioBox(this, wx.ID_ANY, '', choices = this.fontStyleTbl::label) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 8))
					ctrl.SetSelection(iFontStyle)
					ctrl.Bind(wx.EVT_RADIOBOX, this.OnEvent)
					this.radioBox_FontStyle = ctrl
				}
				hbox.Add(wx.StaticText(this, wx.ID_ANY, label = 'Font &Weight'),  wx.SizerFlags().Center().Border(wx.LEFT, 16))
				wx.RadioBox(this, wx.ID_ANY, '', choices = this.fontWeightTbl::label) {|ctrl|
					hbox.Add(ctrl, wx.SizerFlags().Border(wx.LEFT, 8))
					ctrl.SetSelection(iFontWeight)
					ctrl.Bind(wx.EVT_RADIOBOX, this.OnEvent)
					this.radioBox_FontWeight = ctrl
				}
			}
		}
	}
	UpdateFont() = {
		font = this.textCtrl.GetFont()
		font.SetFamily(this.fontFamilyTbl[this.comboBox_FontFamily.GetSelection()].value)
		font.SetPointSize(this.spinCtrl_PointSize.GetValue())
		font.SetWeight(this.fontWeightTbl[this.radioBox_FontWeight.GetSelection()].value)
		font.SetStyle(this.fontStyleTbl[this.radioBox_FontStyle.GetSelection()].value)
		font.SetUnderlined(this.checkBox_Underlined.GetValue())
		font.SetStrikethrough(this.checkBox_Strikethrough.GetValue())
		this.textCtrl.SetFont(font)
	}
	OnEvent(event as wx.Event) = this.UpdateFont()
}

wx.ImplementApp(MyApp())
