#!/usr/bin/env gurax
import(wx)

MyApp = class(wx.App) {
	OnInit() = {
		Frame1(path.BaseName(path.FileName(sys.argv[0])), wx.Point(50, 50), wx.Size(800, 100)).Show()
		Frame2(path.BaseName(path.FileName(sys.argv[0])), wx.Point(50, 50 + 100 * 1), wx.Size(800, 100)).Show()
		Frame3(path.BaseName(path.FileName(sys.argv[0])), wx.Point(50, 50 + 100 * 2), wx.Size(800, 100)).Show()
		true
	}
}

PrepareContent(parent as wx.Window, text as String) = {
	wx.Panel(parent) {|panel|
		vbox = wx.BoxSizer(wx.VERTICAL)
		panel.SetSizer(vbox)
		vbox.Add(wx.StaticText(panel, wx.ID_ANY, text), wx.SizerFlags().Centre())
	}
}

Frame1 = class(wx.Frame) {
	__init__(title as String, pos as wx.Point, size as wx.Size) = {|nil, wx.ID_ANY, title, pos, size|
		statBar = this.CreateStatusBar(4)
		PrepareContent(this, 'wx.StatusBar.SetStatusStyles()')
		statBar.SetStatusStyles([wx.SB_NORMAL, wx.SB_FLAT, wx.SB_RAISED, wx.SB_SUNKEN])
		statBar.SetStatusText('NORMAL', 0)
		statBar.SetStatusText('FLAT', 1)
		statBar.SetStatusText('RAISED', 2)
		statBar.SetStatusText('SUNKEN', 3)
	}
}

Frame2 = class(wx.Frame) {
	__init__(title as String, pos as wx.Point, size as wx.Size) = {|nil, wx.ID_ANY, title, pos, size|
		this.CreateStatusBar(4)
		PrepareContent(this, 'wx.Frame.SetStatusText()')
		this.SetStatusText('Field#1', 0)
		this.SetStatusText('Field#2', 1)
		this.SetStatusText('Field#3', 2)
		this.SetStatusText('Field#4', 3)
	}
}

Frame3 = class(wx.Frame) {
	__init__(title as String, pos as wx.Point, size as wx.Size) = {|nil, wx.ID_ANY, title, pos, size|
		this.CreateStatusBar(4)
		PrepareContent(this, 'wx.StatusBar.SetStatusWidths()')
		statBar = this.GetStatusBar()
		widths = [50, 100, 150, 200]
		statBar.SetStatusWidths(widths)
		widths.Each {|width, i| statBar.SetStatusText(Format('%d', width), i)}
	}
}

wx.ImplementApp(MyApp())
