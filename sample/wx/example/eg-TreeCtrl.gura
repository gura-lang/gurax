#!/usr/bin/env gurax
import(wx)
import(image)

MyApp = class(wx.App) {
	OnInit() = {
		MyFrame(path.BaseName(path.FileName(sys.argv[0])), wx.Point(50, 50), wx.Size(1000, 600)).Show()
		true
	}
}

MyFrame = class(wx.Frame) {
	__init__(title as String, pos as wx.Point, size as wx.Size) = {|nil, wx.ID_ANY, title, pos, size|
		PanelTop(this)
	}
}

PanelTop = class(wx.Panel) {
	textCtrl as wx.TextCtrl
	__init__(parent as wx.Window) = {|parent|
		[imageList, imageIds] = image.InitImageList(image.mini_icons)
		sizer = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(sizer)
		wx.BoxSizer(wx.HORIZONTAL) {|sizer|
			(^sizer).Add(sizer, wx.SizerFlags(3).Expand())
			wx.BoxSizer(wx.VERTICAL) {|sizer|
				(^sizer).Add(sizer, wx.SizerFlags(1).Expand())
				wx.StaticText(this, wx.ID_ANY, 'Default') {|ctrl|
					sizer.Add(ctrl, wx.SizerFlags().Expand())
				}
				wx.TreeCtrl(this, style = wx.TR_DEFAULT_STYLE | wx.TR_EDIT_LABELS) {|ctrl|
					sizer.Add(ctrl, wx.SizerFlags(1).Expand())
					ctrl.SetImageList(imageList);
					this.SetTreeCtrlContent(ctrl, imageList, imageIds)
					this.SetTreeCtrlEventHandler(ctrl)
					treeCtrl = ctrl
				}
				wx.BoxSizer(wx.HORIZONTAL) {|sizer|
					(^sizer).Add(sizer, wx.SizerFlags().Expand())
					wx.ComboBox(this, wx.ID_ANY, style = wx.CB_READONLY, size = wx.Size(150, -1), choices = ['GetSelections']) {|ctrl|
						sizer.Add(ctrl, wx.SizerFlags().Expand())
						ctrl.SetSelection(0)
						comboBox = ctrl
					}
					wx.Button(this, wx.ID_ANY, 'Execute') {|ctrl|
						sizer.Add(ctrl, wx.SizerFlags().Expand().Border(wx.LEFT, 4))
						wx.EVT_BUTTON(ctrl, this.OnButton_Execute, (treeCtrl, comboBox))
					}
				}
			}
			wx.BoxSizer(wx.VERTICAL) {|sizer|
				(^sizer).Add(sizer, wx.SizerFlags(1).Expand())
				wx.StaticText(this, wx.ID_ANY, 'TR_HIDE_ROOT') {|ctrl|
					sizer.Add(ctrl, wx.SizerFlags().Expand())
				}
				wx.TreeCtrl(this, style = wx.TR_DEFAULT_STYLE | wx.TR_EDIT_LABELS | wx.TR_HIDE_ROOT) {|ctrl|
					sizer.Add(ctrl, wx.SizerFlags(1).Expand())
					ctrl.SetImageList(imageList);
					this.SetTreeCtrlContent(ctrl, imageList, imageIds)
					this.SetTreeCtrlEventHandler(ctrl)
				}
			}
			wx.BoxSizer(wx.VERTICAL) {|sizer|
				(^sizer).Add(sizer, wx.SizerFlags(1).Expand())
				wx.StaticText(this, wx.ID_ANY, 'TR_NO_LINES') {|ctrl|
					sizer.Add(ctrl, wx.SizerFlags().Expand())
				}
				wx.TreeCtrl(this, style = wx.TR_DEFAULT_STYLE | wx.TR_EDIT_LABELS | wx.TR_NO_LINES) {|ctrl|
					sizer.Add(ctrl, wx.SizerFlags(1).Expand())
					ctrl.SetImageList(imageList);
					this.SetTreeCtrlContent(ctrl, imageList, imageIds)
					this.SetTreeCtrlEventHandler(ctrl)
				}
			}
			wx.BoxSizer(wx.VERTICAL) {|sizer|
				(^sizer).Add(sizer, wx.SizerFlags(1).Expand())
				wx.StaticText(this, wx.ID_ANY, 'SetIndent(50)') {|ctrl|
					sizer.Add(ctrl, wx.SizerFlags().Expand())
				}
				wx.TreeCtrl(this, style = wx.TR_DEFAULT_STYLE | wx.TR_EDIT_LABELS) {|ctrl|
					sizer.Add(ctrl, wx.SizerFlags(1).Expand())
					ctrl.SetImageList(imageList);
					ctrl.SetIndent(50)
					this.SetTreeCtrlContent(ctrl, imageList, imageIds)
					this.SetTreeCtrlEventHandler(ctrl)
				}
			}
		}
		wx.TextCtrl(this, wx.ID_ANY, style = wx.TE_MULTILINE) {|ctrl|
			(^sizer).Add(ctrl, wx.SizerFlags(1).Expand())
			this.textCtrl = ctrl
		}
	}
	SetTreeCtrlContent(ctrl as wx.TreeCtrl, imageList as wx.ImageList, imageIds as Dict) = {
		ctrl.SetImageList(imageList);
		ctrl.AddRoot('root', image = imageIds[`folder]) {|itemId|
			ctrl.AppendItem(itemId, 'child 1', image = imageIds[`page], data = 'hello 1')
			ctrl.AppendItem(itemId, 'child 2', image = imageIds[`folder]) {|itemId|
				ctrl.AppendItem(itemId, 'child 2-1', data = 'hello 2-1', image = imageIds[`page])
				ctrl.AppendItem(itemId, 'child 2-2', data = 'hello 2-2', image = imageIds[`page])
				ctrl.AppendItem(itemId, 'child 2-3', data = 'hello 2-3', image = imageIds[`page])
				ctrl.AppendItem(itemId, 'child 2-4', data = 'hello 2-4', image = imageIds[`page])
			}
			ctrl.AppendItem(itemId, 'child 3', image = imageIds[`page])
			ctrl.AppendItem(itemId, 'child 4', image = imageIds[`page])
			ctrl.AppendItem(itemId, 'child 5', image = imageIds[`folder]) {|itemId|
				ctrl.AppendItem(itemId, 'child 5-1', image = imageIds[`page])
				ctrl.AppendItem(itemId, 'child 5-2', image = imageIds[`page])
				ctrl.AppendItem(itemId, 'child 5-3', image = imageIds[`page])
				ctrl.AppendItem(itemId, 'child 5-4', image = imageIds[`page])
				ctrl.Expand(itemId)
			}
			ctrl.AppendItem(itemId, 'child 6', image = imageIds[`page])
			if (!ctrl.HasFlag(wx.TR_HIDE_ROOT)) { ctrl.Expand(itemId) }
		}
	}
	SetTreeCtrlEventHandler(ctrl as wx.TreeCtrl) = {
		wx.EVT_TREE_BEGIN_DRAG(ctrl, this.OnTree, ctrl)
		wx.EVT_TREE_BEGIN_RDRAG(ctrl, this.OnTree, ctrl)
		wx.EVT_TREE_END_DRAG(ctrl, this.OnTree, ctrl)
		wx.EVT_TREE_BEGIN_LABEL_EDIT(ctrl, this.OnTree, ctrl)
		wx.EVT_TREE_END_LABEL_EDIT(ctrl, this.OnTree, ctrl)
		wx.EVT_TREE_DELETE_ITEM(ctrl, this.OnTree, ctrl)
		wx.EVT_TREE_GET_INFO(ctrl, this.OnTree, ctrl)
		wx.EVT_TREE_SET_INFO(ctrl, this.OnTree, ctrl)
		wx.EVT_TREE_ITEM_ACTIVATED(ctrl, this.OnTree, ctrl)
		wx.EVT_TREE_ITEM_COLLAPSED(ctrl, this.OnTree, ctrl)
		wx.EVT_TREE_ITEM_COLLAPSING(ctrl, this.OnTree, ctrl)
		wx.EVT_TREE_ITEM_EXPANDED(ctrl, this.OnTree, ctrl)
		wx.EVT_TREE_ITEM_EXPANDING(ctrl, this.OnTree, ctrl)
		wx.EVT_TREE_ITEM_RIGHT_CLICK(ctrl, this.OnTree, ctrl)
		wx.EVT_TREE_ITEM_MIDDLE_CLICK(ctrl, this.OnTree, ctrl)
		wx.EVT_TREE_SEL_CHANGED(ctrl, this.OnTree, ctrl)
		wx.EVT_TREE_SEL_CHANGING(ctrl, this.OnTree, ctrl)
		wx.EVT_TREE_KEY_DOWN(ctrl, this.OnTreeKeyDown, ctrl)
		wx.EVT_TREE_ITEM_GETTOOLTIP(ctrl, this.OnTreeItemGetToolTip, ctrl)
		wx.EVT_TREE_ITEM_MENU(ctrl, this.OnTree, ctrl)
		wx.EVT_TREE_STATE_IMAGE_CLICK(ctrl, this.OnTree, ctrl)
	}
	OnTree(event) = {
		this.PrintTreeEvent(event)
	}
	OnTreeKeyDown(event) = {
		eventName = event.GetEventType().name
		//this.Printf('%s(%s)\n', eventName, event.GetKeyEvent())
		this.Printf('%s(%d)\n', eventName, event.GetKeyCode())
	}
	OnTreeItemGetToolTip(event) = {
		treeCtrl = event.GetEventUserData()
		this.PrintTreeEvent(event)
		itemId = event.GetItem()
		event.SetToolTip(treeCtrl.GetItemText(itemId))
	}
	OnButton_Execute(event) = {
		[treeCtrl, comboBox] = event.GetEventUserData()
		n = comboBox.GetSelection()
		cmdName = comboBox.GetString(n)
		if (cmdName == 'GetSelections') {
			treeCtrl.GetSelections().Each {|itemId|
				this.Printf('%s\n', treeCtrl.GetItemText(itemId))
			}
		}
	}
	PrintTreeEvent(event) = {
		treeCtrl = event.GetEventUserData()
		eventName = event.GetEventType().name
		itemId = event.GetItem()
		this.Printf('%s(\'%s\')\n', eventName, treeCtrl.GetItemText(itemId))
	}
	Printf(format as String, args*) = this.textCtrl.WriteText(Format(format, args*))
}

wx.ImplementApp(MyApp())
