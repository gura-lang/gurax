#!/usr/bin/env gurax
import(wx)

AppMain = class(wx.App) {
	OnInit() = {
		FrameMain(path.BaseName(path.FileName(sys.argv[0])), wx.Point(50, 50), wx.Size(800, 600)).Show()
		true
	}
}

FrameMain = class(wx.Frame) {
	__init__(title as String, pos as wx.Point, size as wx.Size) = {|nil, wx.ID_ANY, title, pos, size|
		PanelMain(this)
	}
}

PanelMain = class(wx.Panel) {
	timerId = 100
	timer as wx.Timer
	textCtrl as wx.TextCtrl
	__init__(parent as wx.Window) = {|parent|
		sizer = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(sizer)
		wx.BoxSizer(wx.VERTICAL) {|sizer|
			(^sizer).Add(sizer, wx.SizerFlags(1).Expand())
			wx.TextCtrl(this, wx.ID_ANY, style = wx.TE_MULTILINE) {|ctrl|
				sizer.Add(ctrl, wx.SizerFlags(5).Expand())
				this.textCtrl = ctrl
			}
			wx.StaticText(this, wx.ID_ANY, '') {|ctrl|
				sizer.Add(ctrl, wx.SizerFlags(1).Expand())
			}
		}
		this.timer = wx.Timer(this, this.timerId)
		wx.EVT_TIMER(this, this.OnTimer, this.timerId)
		this.timer.Start(500)
	}
	OnButton(event as wx.CommandEvent) = {
		Println('Hello!')
	}
	OnTimer(event as wx.TimerEvent) = {
		state = wx.GetMouseState()
		this.Printf('GetX()=%d  GetY()=%d  LeftIsDown()=%d  MiddleIsDown()=%d  RightIsDown()=%d  Aux1IsDown()=%d  Aux2IsDown()=%d\n',
			state.GetX(), state.GetY(), state.LeftIsDown(), state.MiddleIsDown(), state.RightIsDown(), state.Aux1IsDown(), state.Aux2IsDown())
	}
	Printf(format as String, args*) = this.textCtrl.WriteText(Format(format, args*))
}

wx.ImplementApp(AppMain())
