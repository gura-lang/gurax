#!/usr/bin/env gurax
import(wx)

MyApp = class(wx.App) {
	OnInit() = {
		MyFrame(path.BaseName(path.FileName(sys.argv[0])), wx.Point(50, 50), wx.Size(800, 600)).Show()
		true
	}
}

MyFrame = class(wx.Frame) {
	__init__(title as String, pos as wx.Point, size as wx.Size) = {|nil, wx.ID_ANY, title, pos, size|
		PanelTop(this)
	}
}

PanelTop = class(wx.Panel) {
	textCtrl as wx.TextCtrl
	__init__(parent as wx.Window) = {|parent|
		sizerTop = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(sizerTop)
		notebook = wx.Notebook(this, wx.ID_ANY)
		sizerTop.Add(notebook, wx.SizerFlags(1).Expand())
		[(this.Test_Append, 'Append'), (this.Test_Set, 'Set'), (this.Test_Insert, 'Insert')].Each {|tuple|
			[TestFunc, label] = tuple
			panel = wx.Panel(notebook)
			notebook.AddPage(panel, label)
			sizer = wx.FlexGridSizer(2, wx.Size(16, 4))
			panel.SetSizer(sizer)
			//sizerTop.Add(sizer, wx.SizerFlags(1).Expand())
			sizer.Add(wx.StaticText(panel, wx.ID_ANY, 'ListBox'), wx.SizerFlags())
			sizer.AddGrowableCol(1, 1)
			sizer.AddGrowableRow(0, 1)
			wx.ListBox(panel, wx.ID_ANY) {|ctrl|
				sizer.Add(ctrl, wx.SizerFlags().Expand())
				TestFunc(ctrl)
				wx.EVT_LISTBOX(ctrl, this.OnListBox, ctrl)
			}
			sizer.Add(wx.StaticText(panel, wx.ID_ANY, 'ComboBox'), wx.SizerFlags())
			wx.ComboBox(panel, wx.ID_ANY) {|ctrl|
				sizer.Add(ctrl, wx.SizerFlags().Expand())
				TestFunc(ctrl)
				wx.EVT_COMBOBOX(ctrl, this.OnComboBox, ctrl)
				ctrl.SetSelection(0)
			}
			sizer.Add(wx.StaticText(panel, wx.ID_ANY, 'Choice'), wx.SizerFlags())
			wx.Choice(panel, wx.ID_ANY) {|ctrl|
				sizer.Add(ctrl, wx.SizerFlags().Expand())
				TestFunc(ctrl)
				wx.EVT_CHOICE(ctrl, this.OnChoice, ctrl)
				ctrl.SetSelection(0)
			}
		}
		wx.TextCtrl(this, wx.ID_ANY, style = wx.TE_MULTILINE) {|ctrl|
			sizerTop.Add(ctrl, wx.SizerFlags(1).Expand())
			this.textCtrl = ctrl
		}
	}
	Test_Append(ctrl as wx.Window) = {
		ctrl.Append('(String)')
		ctrl.Append('(String)')
		ctrl.Append('(String, String)', 'User data (String, String)')
		ctrl.Append(['(List) #1', '(List) #2', '(List) #3'])
		ctrl.Append(['(List, List) #1', '(List, List) #2', '(List, List) #3'],
			['User data (List, List) #1', 'User data (List, List) #2', 'User data (List, List) #3'])
		ctrl.SetClientData(1, 'User data (String)')
		ctrl.SetClientData(4, 'User data (List) #2')
	}
	Test_Set(ctrl as wx.Window) = {
		ctrl.Set(['removed #1', 'removed #2', 'removed #3'],
			['User data (List, List) #1', 'User data (List, List) #2', 'User data (List, List) #3'])
		ctrl.Set(['(List, List) #1', '(List, List) #2', '(List, List) #3'],
			['User data (List, List) #1', 'User data (List, List) #2', 'User data (List, List) #3'])
	}
	Test_Insert(ctrl as wx.Window) = {
		ctrl.Set(['(List, List) #1', '(List, List) #2', '(List, List) #3'],
			['User data (List, List) #1', 'User data (List, List) #2', 'User data (List, List) #3'])
		ctrl.Insert('Inserted (String)', 1)
		ctrl.Insert('Inserted (String, String)', 0, 'User data (String, String)')
		ctrl.Insert(['Inserted (List)', 'Inserted (List)', 'Inserted (List)'], 4)
		ctrl.Insert(['Inserted (List, List)', 'Inserted (List, List)', 'Inserted (List, List)'], 6,
			['User data (List, List)', 'User data (List, List)', 'User data (List, List)'])
	}
	OnListBox(event) = {
		ctrl = event.GetEventUserData()
		pos = event.GetSelection()
		this.textCtrl.WriteText(Format('ListBox(Position=%d, UserData=%s)\n', pos, ctrl.GetClientData(pos)))
	}
	OnComboBox(event) = {
		ctrl = event.GetEventUserData()
		pos = event.GetSelection()
		this.textCtrl.WriteText(Format('ComboBox(Position=%d, UserData=%s)\n', pos, ctrl.GetClientData(pos)))
	}
	OnChoice(event) = {
		ctrl = event.GetEventUserData()
		pos = event.GetSelection()
		this.textCtrl.WriteText(Format('Choice(Position=%d, UserData=%s)\n', pos, ctrl.GetClientData(pos)))
	}
}

wx.ImplementApp(MyApp())
