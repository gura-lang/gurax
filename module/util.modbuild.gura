#!/usr/bin/env gurax
//==============================================================================
// util.modbuild.gura
//==============================================================================
import(argopt)

//------------------------------------------------------------------------------
// Builder
//------------------------------------------------------------------------------
Builder = class {
	targetName = ''
	pathNameGurd = ''
	pathNamesSrc = []
	pathNamesScript = []
	pathNamesObj = []
	dirNameOut = 'gnuc'
	dirNamesInc = []
	dirNamesLib = []
	cflags = []
	ldflags = []
	libs = []
	opt = %{}
	cflagsBase = ['-Wall', '-std=c++14', '-DGURAX_MODULE_SEPARATED', '-fPIC']
	ldflagsBase = ['-shared']
	cflagsCompile = []
	cflagsPreCompile = ['-x', 'c++-header']
	libsBase = ['stdc++', 'guraxcore']
	precompile:String = nil
	__init__(targetName:String) = {
		this.targetName = targetName
		if (sys.platform == `darwin) {
			this.ldflagsBase.Add('-install_name', this.targetName + '.gurd')
		}
	}
	ShouldProcess(pathNameTgt:String, pathNamesDep+:String) = {
		!fs.Exists(pathNameTgt) || \
			(fs.Stat(pathNameTgt).mtime < fs.Stat(pathNamesDep):*mtime).Or()
	}
	Exec(pathName:String, args*:String) = {
		this.opt.Get('verbose') && Printf('%s %s\n', pathName, args.Join(' '))
		rtn = (os.Exec(pathName, args*) == 0)
		rtn
	}
	PreCompile() = {
		pathNameSrc = this.precompile
		pathNameObj = pathNameSrc + '.gch'
		!this.ShouldProcess(pathNameObj, pathNameSrc) && return(true)
		Printf('-- compiling: %s\n', pathNameSrc)
		this.Exec('gcc', this.cflagsBase*, this.cflagsPreCompile*, this.cflags*,
				  ('-I' + sys.dirInclude), ('-I' + this.dirNamesInc)*,
				  pathNameSrc)
		
	}
	Compile(pathNameObj:String, pathNameSrc:String) = {
		!this.ShouldProcess(pathNameObj, pathNameSrc) && return(true)
		Printf('-- compiling: %s\n', pathNameSrc)
		this.Exec('gcc', this.cflagsBase*, this.cflagsCompile*, this.cflags*,
				  ('-I' + sys.dirInclude), ('-I' + this.dirNamesInc)*,
				  '-o' + pathNameObj, '-c', pathNameSrc)
	}
	Link(pathNameGurd:String, pathNamesObj+:String) = {
		!this.ShouldProcess(pathNameGurd, pathNamesObj*) && return
		if (this.Exec('gcc', this.ldflagsBase*, this.ldflags*,
					  '-L' + sys.dirLib, ('-L' + this.dirNamesLib)*,
					  '-o' + pathNameGurd, pathNamesObj*,
					  ('-l' + this.libsBase)*, ('-l' + this.libs)*)) {
			Printf('-- generated: %s\n', pathNameGurd)
		}
	}
	Command_build() = {
		//errorFlag = false
		this.precompile && !this.PreCompile() && return
		for (pathNameObj in this.pathNamesObj, pathNameSrc in this.pathNamesSrc) {
			!this.Compile(pathNameObj, pathNameSrc) && return
		}
		this.Link(this.pathNameGurd, this.pathNamesObj*)
	}
	Command_clean() = {
		fs.Remove(this.pathNameGurd)
		fs.Remove(this.pathNamesObj)
		this.precompile && fs.Remove(this.precompile + '.gch')
		Printf('-- cleaned\n')
	}
	Command_install() = {
		for (pathName in ([this.pathNameGurd] |+| this.pathNamesScript)) {
			Printf('-- installing: %s\n', pathName)
			fs.Copy(pathName, path.Join(sys.dirModule, 'site')):overwrite
		}
	}
	Run() = {
		if (this.precompile) {
			//this.cflagsCompile.Add('-install', this.precompile)
		}
		this.pathNameGurd = path.Join(this.dirNameOut, this.targetName + '.gurd')
		this.pathNamesObj = path.Join(this.dirNameOut, path.BaseName(this.pathNamesSrc)::Replace('/', '_') + '.o')
		!fs.Exists(this.dirNameOut) && fs.CreateDir(this.dirNameOut)
		[argv, this.opt] = argopt.Parser {|p|
			p.AddFlag('verbose', 'v', 'verbose mode')
			p.Parse(sys.argv)
		}
		if (argv.IsEmpty()) {
			this.Command_build()
		} elsif (argv[0] == 'clean') {
			this.Command_clean()
		} elsif (argv[0] == 'install') {
			this.Command_install()
		} else {
			sys.cerr.Printf('unknown command: %s\n', argv[0])
			sys.Exit(1)
		}
	}
}
